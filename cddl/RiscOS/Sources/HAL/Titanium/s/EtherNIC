;
; CDDL HEADER START
;
; The contents of this file are subject to the terms of the
; Common Development and Distribution License (the "Licence").
; You may not use this file except in compliance with the Licence.
;
; You can obtain a copy of the licence at
; cddl/RiscOS/Sources/HAL/Titanium/LICENCE.
; See the Licence for the specific language governing permissions
; and limitations under the Licence.
;
; When distributing Covered Code, include this CDDL HEADER in each
; file and include the Licence file. If applicable, add the
; following below this CDDL HEADER, with the fields enclosed by
; brackets "[]" replaced with your own identifying information:
; Portions Copyright [yyyy] [name of copyright owner]
;
; CDDL HEADER END
;
;
; Copyright 2014 Elesar Ltd.  All rights reserved.
; Use is subject to license terms.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:OSEntries
        GET     Hdr:HALDevice
        GET     Hdr:VideoDevice

        GET     hdr.RegMap
        GET     hdr.StaticWS

        AREA    |EtherNIC$$Code|, CODE, READONLY, PIC

        EXPORT  EtherNICDeviceInit
        EXPORT  EtherNICInit
        IMPORT  memcpy

; EtherNICInit
EtherNICInit ROUT
        Push    "lr"

        ; Turn on the GMAC controller clock
        LDR     a1, CMCoreL3INIT_LogBase
        LDR     a2, =CM_L3INIT_GMAC_GMAC_CLKCTRL_CLKSEL_RFT_L3_ICLK :OR: \
                     CM_CLKCTRL_MODULEMODE_ENABLED
        STR     a2, [a1, #CM_L3INIT_GMAC_GMAC_CLKCTRL]

        ; Map in the GMAC switch which comprises NIC+SRAM buffer
        MOV     a1, #0
        LDR     a2, =GMAC_PhysBase
        MOV     a3, #GMAC_PhysSize
        CallOS  OS_MapInIO
        STR     a1, NIC_LogBase

        Pull    "pc"

; EtherNICDeviceInit
EtherNICDeviceInit ROUT
        Push    "lr"
        
        ADRL    a1, EtherNICDevice
        ADR     a2, EtherNICDeviceTemplate
        MOV     a3, #HALDeviceSize
        BL      memcpy

        LDR     a2, NIC_LogBase
        STR     a2, [a1, #HALDevice_Address]

        MOV     a2, a1
        MOV     a1, #0                  ; No flags
        CallOS  OS_AddDevice

        Pull    "pc"

EtherNICDeviceTemplate
        DCW     HALDeviceType_Comms + HALDeviceComms_EtherNIC
        DCW     HALDeviceID_EtherNIC_CPSW
        DCD     HALDeviceBus_Sys + HALDeviceSysBus_NoC
        DCD     0                       ; API version 0
        DCD     EtherNICDevice_Desc
        DCD     0                       ; Address of CPSW - filled later
        %       12                      ; Reserved
        DCD     EtherNICDevice_Activate
        DCD     EtherNICDevice_Deactivate
        DCD     EtherNICDevice_Reset
        DCD     EtherNICDevice_Sleep
        DCD     DevNoGMAC               ; Device number of CPSW (+3 others)
        DCD     0
        %       8
        ASSERT  (.-EtherNICDeviceTemplate) = HALDeviceSize
        ASSERT  HALDeviceSize = ?EtherNICDevice

EtherNICDevice_Desc
        DCB     "Common platform ethernet switch", 0
        ALIGN

EtherNICDevice_Activate
        MOV     a1, #1
EtherNICDevice_Deactivate
EtherNICDevice_Reset
        MOV     pc, lr

EtherNICDevice_Sleep
        MOV     a1, #0
        MOV     pc, lr

        END
