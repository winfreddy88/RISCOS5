;
; CDDL HEADER START
;
; The contents of this file are subject to the terms of the
; Common Development and Distribution License (the "Licence").
; You may not use this file except in compliance with the Licence.
;
; You can obtain a copy of the licence at
; cddl/RiscOS/Sources/HAL/Titanium/LICENCE.
; See the Licence for the specific language governing permissions
; and limitations under the Licence.
;
; When distributing Covered Code, include this CDDL HEADER in each
; file and include the Licence file. If applicable, add the
; following below this CDDL HEADER, with the fields enclosed by
; brackets "[]" replaced with your own identifying information:
; Portions Copyright [yyyy] [name of copyright owner]
;
; CDDL HEADER END
;
;
; Copyright 2014 Elesar Ltd.  All rights reserved.
; Use is subject to license terms.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System

        AREA    |Utils$$Code|, CODE, READONLY, PIC

        EXPORT  memcpy
        EXPORT  memset
        EXPORT  _ll_shift_l

; _ll_shift_l
; => a1 = lo
;    a2 = hi
;    a3 = shift
; <= a1 = lo
;    a2 = hi
_ll_shift_l
        RSBS    a4, a3, #32
        MOVHI   a2, a2, LSL a3
        ORRHI   a2, a2, a1, LSR a4
        MOVHI   a1, a1, LSL a3
        SUBLS   a4, a3, #32
        MOVLS   a2, a1, LSL a4
        MOVLS   a1, #0
        MOV     pc, lr

; memcpy
; => a1 = dst
;    a2 = src
;    a3 = length
; <= a1 = dst
memcpy ROUT
        TEQ     a3, #0
        MOVEQ   pc, lr
10
        SUBS    a3, a3, #1
        LDRB    a4, [a2, a3]
        STRB    a4, [a1, a3]
        BNE     %BT10

        MOV     pc, lr

; memset
; => a1 = dst
;    a2 = char
;    a3 = length
; <= a1 = dst
memset ROUT
        TEQ     a3, #0
        MOVEQ   pc, lr
10
        SUBS    a3, a3, #1
        STRB    a2, [a1, a3]
        BNE     %BT10

        MOV     pc, lr

        END
