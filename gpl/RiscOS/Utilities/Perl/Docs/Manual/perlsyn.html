<!-- $RCSfile: perlsyn.html,v $$Revision: 1.1.1.1 $$Date: 2003/12/11 15:47:22 $ -->
<!-- $Log: perlsyn.html,v $
<!-- Revision 1.1.1.1  2003/12/11 15:47:22  bavison
<!-- Sources to RISC OS Perl 5 port 1.51 discovered in a dusty corner of the
<!-- Internet, given a new build structure and makefile, and knocked into C99
<!-- shape. Various functions missing from the original sources have been
<!-- recreated either in assembler or C, by disassembling the binary from
<!-- RiscOS.Apps.!Perl.Perl.
<!-- Tested in a desktop machine ROM build.
<!-- -->
<HTML>
<TITLE> PERLSYN </TITLE>
<h2>NAME</h2>
perlsyn - Perl syntax
<p><h2>DESCRIPTION</h2>
A Perl script consists of a sequence of declarations and statements.
The only things that need to be declared in Perl are report formats
and subroutines.  See the sections below for more information on those
declarations.  All uninitialized user-created objects are assumed to
start with a null or 0 value until they are defined by some explicit
operation such as assignment.  (Though you can get warnings about the
use of undefined values if you like.)  The sequence of statements is
executed just once, unlike in <B>sed</B> and <B>awk</B> scripts, where the
sequence of statements is executed for each input line.  While this means
that you must explicitly loop over the lines of your input file (or
files), it also means you have much more control over which files and
which lines you look at.  (Actually, I'm lying--it is possible to do an
implicit loop with either the 
<A HREF="perlrun.html#perlrun_353">-n</A>
 or 
<A HREF="perlrun.html#perlrun_354">-p</A>
 switch.  It's just not the
mandatory default like it is in <B>sed</B> and <B>awk</B>.)
<p>Perl is, for the most part, a free-form language.  (The only
exception to this is format declarations, for obvious reasons.) Comments
are indicated by the "#" character, and extend to the end of the line.  If
you attempt to use <B>/* */</B> C-style comments, it will be interpreted
either as division or pattern matching, depending on the context, and C++
<B>//</B> comments just look like a null regular expression, So don't do
that.
<p>A declaration can be put anywhere a statement can, but has no effect on
the execution of the primary sequence of statements--declarations all
take effect at compile time.  Typically all the declarations are put at
the beginning or the end of the script.
<p>As of Perl 5, declaring a subroutine allows a subroutine name to be used
as if it were a list operator from that point forward in the program.  You
can declare a subroutine without defining it by saying just
<p><pre>
        sub myname;
        $me = myname $0                 or die "can't get myname";
</pre>
Note that it functions as a list operator though, not a unary
operator, so be careful to use <B>or</B> instead of <B>||</B> there.
<p>Subroutines declarations can also be imported by a 
<A HREF="perlfunc.html#perlfunc_263">use</A>
 statement.
<p>Also as of Perl 5, a statement sequence may contain declarations of
lexically scoped variables, but apart from declaring a variable name,
the declaration acts like an ordinary statement, and is elaborated within
the sequence of statements as if it were an ordinary statement.
<p><h3>Simple statements</h3>
The only kind of simple statement is an expression evaluated for its
side effects.  Every simple statement must be terminated with a
semicolon, unless it is the final statement in a block, in which case
the semicolon is optional.  (A semicolon is still encouraged there if the
block takes up more than one line, since you may add another line.)
Note that there are some operators like <B>eval {}</B> and <B>do {}</B> that look
like compound statements, but aren't (they're just TERMs in an expression), 
and thus need an explicit termination
if used as the last item in a statement.
<p>Any simple statement may optionally be followed by a <I>SINGLE</I> modifier,
just before the terminating semicolon (or block ending).  The possible
modifiers are:
<p><pre>
        if EXPR
        unless EXPR
        while EXPR
        until EXPR
</pre>
The <B>if</B> and <B>unless</B> modifiers have the expected semantics,
presuming you're a speaker of English.  The <B>while</B> and <B>until</B>
modifiers also have the usual "while loop" semantics (conditional
evaluated first), except when applied to a do-BLOCK (or to the
now-deprecated do-SUBROUTINE statement), in which case the block
executes once before the conditional is evaluated.  This is so that you
can write loops like:
<p><pre>
        do {
        $_ = &lt;STDIN&gt;;
        ...
        } until $_ eq ".\n";
</pre>
See perlfunc/do.  Note also that the loop control
statements described later will <I>NOT</I> work in this construct, since
modifiers don't take loop labels.  Sorry.  You can always wrap
another block around it to do that sort of thing.)
<p><h3>Compound statements</h3>
In Perl, a sequence of statements that defines a scope is called a block.
Sometimes a block is delimited by the file containing it (in the case
of a required file, or the program as a whole), and sometimes a block
is delimited by the extent of a string (in the case of an eval).
<p>But generally, a block is delimited by curly brackets, also known as braces.
We will call this syntactic construct a BLOCK.
<p>The following compound statements may be used to control flow:
<p><pre>
        if (EXPR) BLOCK
        if (EXPR) BLOCK else BLOCK
        if (EXPR) BLOCK elsif (EXPR) BLOCK ... else BLOCK
        LABEL while (EXPR) BLOCK
        LABEL while (EXPR) BLOCK continue BLOCK
        LABEL for (EXPR; EXPR; EXPR) BLOCK
        LABEL foreach VAR (ARRAY) BLOCK
        LABEL BLOCK continue BLOCK
</pre>
Note that, unlike C and Pascal, these are defined in terms of BLOCKs,
not statements.  This means that the curly brackets are <I>required</I>--no
dangling statements allowed.  If you want to write conditionals without
curly brackets there are several other ways to do it.  The following
all do the same thing:
<p><pre>
        if (!open(FOO)) { die "Can't open $FOO: $!"; }
        die "Can't open $FOO: $!" unless open(FOO);
        open(FOO) or die "Can't open $FOO: $!"; # FOO or bust!
        open(FOO) ? 'hi mom' : die "Can't open $FOO: $!";
                        # a bit exotic, that last one
</pre>
The <B>if</B> statement is straightforward.  Since BLOCKs are always
bounded by curly brackets, there is never any ambiguity about which
<B>if</B> an <B>else</B> goes with.  If you use <B>unless</B> in place of <B>if</B>,
the sense of the test is reversed.
<p>The <B>while</B> statement executes the block as long as the expression is
true (does not evaluate to the null string or 0 or "0").  The LABEL is
optional, and if present, consists of an identifier followed by a
colon.  The LABEL identifies the loop for the loop control statements

<A HREF="perlfunc.html#perlfunc_179">next</A>
, 
<A HREF="perlfunc.html#perlfunc_161">last</A>
, and 
<A HREF="perlfunc.html#perlfunc_202">redo</A>
 (see below).  If there is a <B>continue</B>
BLOCK, it is always executed just before the conditional is about to be
evaluated again, just like the third part of a <B>for</B> loop in C.
Thus it can be used to increment a loop variable, even when the loop
has been continued via the 
<A HREF="perlfunc.html#perlfunc_179">next</A>
 statement (which is similar to the C
<B>continue</B> statement).
<p>If the word <B>while</B> is replaced by the word <B>until</B>, the sense of the
test is reversed, but the conditional is still tested before the first
iteration.
<p>In either the <B>if</B> or the <B>while</B> statement, you may replace "(EXPR)"
with a BLOCK, and the conditional is true if the value of the last
statement in that block is true.  (This feature continues to work in Perl
5 but is deprecated.  Please change any occurrences of "if BLOCK" to
"if (do BLOCK)".)
<p>The C-style <B>for</B> loop works exactly like the corresponding <B>while</B> loop:
<p><pre>
        for ($i = 1; $i &lt; 10; $i++) {
        ...
        }
</pre>
is the same as
<p><pre>
        $i = 1;
        while ($i &lt; 10) {
        ...
        } continue {
        $i++;
        }
</pre>
The foreach loop iterates over a normal list value and sets the
variable VAR to be each element of the list in turn.  The variable is
implicitly local to the loop (unless declared previously with 
<A HREF="perlfunc.html#perlfunc_178">my</A>
),
and regains its former value upon exiting the loop.  The <B>foreach</B>
keyword is actually a synonym for the <B>for</B> keyword, so you can use
<B>foreach</B> for readability or <B>for</B> for brevity.  If VAR is omitted, $_
is set to each value.  If ARRAY is an actual array (as opposed to an
expression returning a list value), you can modify each element of the
array by modifying VAR inside the loop.  Examples:
<p><pre>
        for (@ary) { s/foo/bar/; }
</pre>
<pre>
        foreach $elem (@elements) {
        $elem *= 2;
        }
</pre>
<pre>
        for ((10,9,8,7,6,5,4,3,2,1,'BOOM')) {
        print $_, "\n"; sleep(1);
        }
</pre>
<pre>
        for (1..15) { print "Merry Christmas\n"; }
</pre>
<pre>
        foreach $item (split(/:[\\\n:]*/, $ENV{'TERMCAP'})) {
        print "Item: $item\n";
        }
</pre>
A BLOCK by itself (labeled or not) is semantically equivalent to a loop
that executes once.  Thus you can use any of the loop control
statements in it to leave or restart the block.  The <B>continue</B> block
is optional.  This construct is particularly nice for doing case
structures.
<p><pre>
        SWITCH: {
        if (/^abc/) { $abc = 1; last SWITCH; }
        if (/^def/) { $def = 1; last SWITCH; }
        if (/^xyz/) { $xyz = 1; last SWITCH; }
        $nothing = 1;
        }
</pre>
There is no official switch statement in Perl, because there are
already several ways to write the equivalent.  In addition to the
above, you could write
<p><pre>
        SWITCH: {
        $abc = 1, last SWITCH  if /^abc/;
        $def = 1, last SWITCH  if /^def/;
        $xyz = 1, last SWITCH  if /^xyz/;
        $nothing = 1;
        }
</pre>
(That's actually not as strange as it looks one you realize that you can
use loop control "operators" within an expression,  That's just the normal
C comma operator.)
<p>or
<p><pre>
        SWITCH: {
        /^abc/ &amp;&amp; do { $abc = 1; last SWITCH; };
        /^def/ &amp;&amp; do { $def = 1; last SWITCH; };
        /^xyz/ &amp;&amp; do { $xyz = 1; last SWITCH; };
        $nothing = 1;
        }
</pre>
or formatted so it stands out more as a "proper" switch statement:
<p><pre>
        SWITCH: {
        /^abc/      &amp;&amp; do { 
                            $abc = 1; 
                            last SWITCH; 
                       };
</pre>
<pre>
        /^def/      &amp;&amp; do { 
                            $def = 1; 
                            last SWITCH; 
                       };
</pre>
<pre>
        /^xyz/      &amp;&amp; do { 
                            $xyz = 1; 
                            last SWITCH; 
                        };
        $nothing = 1;
        }
</pre>
or
<p><pre>
        SWITCH: {
        /^abc/ and $abc = 1, last SWITCH;
        /^def/ and $def = 1, last SWITCH;
        /^xyz/ and $xyz = 1, last SWITCH;
        $nothing = 1;
        }
</pre>
or even, horrors,
<p><pre>
        if (/^abc/)
        { $abc = 1 }
        elsif (/^def/)
        { $def = 1 }
        elsif (/^xyz/)
        { $xyz = 1 }
        else
        { $nothing = 1 }
</pre>
