	AREA	|Asm$$Code|, CODE, READONLY
; We don't want to use stubs here...

	ENTRY

	SWI	&10 ; OS_GetEnv
	MOV	r13,r1
	MOV	r0,#17
	ADR	r1,file
	SWI	&20008 ; XOS_File
	BVS	exit
	TEQ	r0,#1
	BNE	notfound
	ADR	r1,grep
	ADD	r1,r1,r4
	ADD	r1,r1,#1024
	CMP	r1,r13
	ADRHI	r0,noroom
	BHI	exit
	MOV	r0,#16
	ADR	r1,file
	ADR	r2,grep
	MOV	r3,#0
	SWI	&20008
	BVS	exit
	ADR	r8,grep
	ADD	r9,r8,r4
	ADD	r9,r9,#3
	BIC	r9,r9,#3
	ADR	r0,shuffle
	ADR	r1,shuffle_end
	MOV	r2,r9
move1	LDR	r14,[r0],#4
	TEQ	r0,r1
	STR	r14,[r2],#4
	BNE	move1
	MOV	r0,#1
	MOV	r1,r9
	ADD	r2,r9,#shuffle_end-shuffle-4
	SWI	&2006E ; XOS_SynchroniseCodeAreas
	MOV	pc,r9

shuffle	MOV	r7,#&8000
move2	LDR	r14,[r8],#4
	TEQ	r8,r9
	STR	r14,[r7],#4
	BNE	move2
	MOV	r0,#1
	MOV	r1,#&8000
	SUB	r2,r9,#4
	SWI	&2006E
	MOV	pc,#&8000
shuffle_end

noroom	& 0
	= "Not enough memory",0

file	= "Run:grep",0
	ALIGN

notfound
	MOV	r2,r0
	ADR	r1,file+4
	MOV	r0,#19
	SWI	&20008
;	B	exit

exit	LDR	r1,abex
	TEQ	r0,#0
	MOVEQ	r2,#0
	MOVNE	r2,#1
	MOVVS	r2,#2
	SWI	&11 ; OS_Exit
abex	= "ABEX"

grep
	END
