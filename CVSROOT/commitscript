#!/bin/perl
#
# $Id: commitscript,v 1.2 2008/06/10 12:42:01 rool Exp $
#
# commitscript is the script that determines whether the user attempting to
# commit files is permitted to commit to the directory they are using.
#
# The script returns non-zero if the commit is to be faulted (and sends
# e-mail to $admin to notify them of the failure)
# The script returns zero to permit the commit
#
# This script reads the file CVSROOT/Access.
#
# CLI parameters: CVS_Username Directory List_of_files...

use lib '/home/rool/bin/perllib';
use Pace::CvsAccess;
use Pace::CvsDirectory;
use Pace::CvsEmail;
use Pace::CvsRepository;

my $dont_send_mail_if_stable_tag = 1;
my $debug = 0;
my $admin='Code Submissions <code@riscosopen.org>';
($cvsuser,$repos,@files)=@ARGV;
$repos = &strip_cvsroot_from_absolute_path($ENV{'CVSROOT'}, $repos);
my ($tagtype, $dirtag) = &directory_tag();
exit 0 if $repos=~m'^Products(/|$)' && !$dirtag;

# First, get the tags which are currently being used.  For efficiency,
# we read the Entries file ourself, instead of calling cvs.
my %entries = map { $_->type eq '/' ? ($_->name, $_->tagdate) : () } &directory_entries();

# Validate the information we have read
for (keys %entries) {
  &fail_it("Internal error reading CVS/Entries file\n") if ($entries{$_} && $entries{$_} !~ /^[A-Z](.*)$/);
  $entries{$_} &&= $1;
}

# Now check the tags the user is allowed to commit to against the tags
# the user is trying to commit to.
foreach $file (@files) {
  my $spec=$entries{$file};
  my $tag=$spec || 'TRUNK';
  unless (&check_access_details($cvsuser,$tag,$repos,$file)) {
    &fail_it("\nYou cannot commit the file '$file' to ".
            ($spec?"branch $tag":"the trunk")." because you ($cvsuser) are\n".
            "not in the correct access list.\n", $tag, $spec);
  }
}
exit 0;

sub fail_it {
  my ($msg, $tag, $spec) = @_;

  unless ($debug) { # Do not bother sending diagnostic mail if we are in debug mode
    my $email;

    if ($tag || $spec) {
      $email = "\nThe user $cvsuser has been prevented from committing files into\n".
               "the repository according to the rules in the Access file\n\n".
               "Repository: ".$ENV{'CVSROOT'}."\n".
               "Directory : $repos\n".
               "Files:\n";
      $email .= "  $_ -> $entries{$_}\n" for (keys %entries);
    }
    else {
      $email = $msg;
    }
    &send_email( From => $admin, To => $admin, Subject => "CVS ACLs: $cvsuser ($repos)", Text => $email) unless $tag =~ /\_stable$/;
  }

  die($msg."\n");
}
