#!/usr/bin/env awk
#
# Copyright 2009 Castle Technology Ltd
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Change _Version token in Messages or Desc file
# to something like 9.87 (01-Jan-1987) from the VersionNum file

BEGIN {
    while (getline < "VersionNum" > 0) {
        split($0, Version)
        if (Version[2] == "Module_FullVersion") {
            split($0, VString, "\"")
       }
        if (Version[2] == "Module_ApplicationDate") {
            split($0, VDate, "\"")
       }
    }
}

/^_Version/ {
    if (descmode) {
      # For use in FrontEnd description files
      printf("  version    \"%s (%s)\";\n", VString[2], VDate[2])
    } else {
      if (obeymode) {
        # For use in obey files
        printf("| Version %s (%s)\n", VString[2], VDate[2]) 
      } else {
        # For use in messages files
        printf("_Version:%s (%s)\n", VString[2], VDate[2])
      }
    }
    done = 1
    next
}

{ print }

END {
    if (!done) {
        printf("_Version:%s (%s)\n", VString[2], VDate[2]);
    }
}
