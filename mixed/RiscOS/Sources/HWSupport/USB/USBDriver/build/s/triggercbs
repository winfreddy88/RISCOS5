; Copyright 2003 Tematic Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;

; trigger callbacks by calling OS_LeaveOS and OS_EnterOS

        GET     Hdr:ListOpts
        OPT     OptNoList
        GET     Hdr:PublicWS
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:OSRSI6
        GET     Hdr:Proc

        AREA    |C$$data|, DATA
ptr_IRQsema
        DCD     0   ; Cached IRQsema ptr

        AREA    |C$$code|, CODE, READONLY

        EXPORT  get_ptr_IRQsema
get_ptr_IRQsema
        LDR     r1, [sl,#-536]       ; Get relocation
        LDR     r3, =ptr_IRQsema
        LDR     r0, [r3, r1]!
        CMP     r0, #0
        MOVNE   pc, lr
        MOV     r0, #6
        MOV     r1, #0
        MOV     r2, #OSRSI6_IRQsema
        MOV     ip, lr
        SWI     XOS_ReadSysInfo
        MOVVS   r2, #0
        MOVS    r0, r2
        MOVEQ   r0, #Legacy_IRQsema
        STR     r0, [r3]
        MOV     pc, ip

        EXPORT  triggercbs
triggercbs
        Entry
        BL      get_ptr_IRQsema
        LDR     lr, [r0]
        MOVS    lr, lr
        EXIT    NE                   ; NZ is within IRQ.. so no CB allowed
        SWI     OS_LeaveOS
        SWI     OS_EnterOS
        EXIT

        LTORG

        END
