; Copyright 2012 Castle Technology Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;
 [ Debug
; a2-> null terminated string
DebugTXS    ROUT
        STMFD   sp!, {a1-a2,lr}
        SUB     a2,a2,#1
1       LDRB    a1, [a2,#1]!
        TEQ     a1, #&0
        LDMEQFD sp!, {a1-a2,pc}
        BL      DebugTX
        B       %BT1

DebugHexTX
       stmfd    r13!, {r0-r3,lr}
       b        jbdt1
DebugHexTX2
       stmfd    r13!, {r0-r3,lr}
       b        jbdt2
DebugHexTX4
       stmfd    r13!, {r0-r3,lr}
       mov      r0,r0,ror #24          ; hi byte
       bl       jbdtxh
       mov      r0,r0,ror #24
       bl       jbdtxh
jbdt2
       mov      r0,r0,ror #24
       bl       jbdtxh
       mov      r0,r0,ror #24
jbdt1
       bl       jbdtxh
       mov      r0,#' '
       bl       DebugTX
       ldmfd    r13!, {r0-r3,pc}

DebugTXStrInline
       stmfd    r13!, {r0-r3}          ; lr points to prinstring, immediately
                                       ; following call, null terminated
       sub      r3,lr,#1
1      ldrb     r0,[r3,#1]!            ; pop next char, auto incr
       teq      r0,#0                  ; terminating null
       biceq    lr,r3,#3               ; round down address
       ldmeqfd  r13!,{r0-r3}
       addeq    pc,lr,#4               ; return to next word
       bl       DebugTX            ; else send, then
       b        %bt1                   ; loop

jbdtxh stmfd    r13!,{r0-r3,lr}        ; print byte as hex
       and      a4,a1,#&f              ; get low nibble
       and      a1,a1,#&f0             ; get hi nibble
       mov      a1,a1,lsr #4           ; shift to low nibble
       cmp      a1,#&9                 ; 9?
       addle    a1,a1,#&30
       addgt    a1,a1,#&37             ; convert letter if needed
       bl       DebugTX
       cmp      a4,#9
       addle    a1,a4,#&30
       addgt    a1,a4,#&37
       bl       DebugTX
       ldmfd    r13!,{r0-r3,pc}

DebugTX
     [ Debug_DADebug
       ldr      pc, dadebug_writec
     |
       stmfd    r13!,{r0-r3,r8,r9,lr}
       mov      r8, #OSHW_CallHAL
       mov      r9, #EntryNo_HAL_DebugTX
       SWI      XOS_Hardware
       ldmfd    r13!,{r0-r3,r8,r9,pc}
     ]

 ]
       END
