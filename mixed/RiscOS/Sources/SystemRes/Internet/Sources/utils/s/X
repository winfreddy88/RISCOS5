; Copyright 1998 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;
; Perform the specified CLI command and swallow any error. If there
; was an error, copy the error string into the system variable
; X$Error, unless X$Error is already set to something, in which case
; the error is ignored. Thus, a chain of X commands will return only
; the first error in X$Error.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Proc

        AREA    |X$$Code|, CODE, READONLY, PIC

	Entry
X
        MOV     R0,R1
        SWI     XOS_CLI
        MOVVC   PC,R14
        ADD     R11,R0,#4
        ADR     R0,rcsysvar
        ADR     R1,zero
        MOV     R2,#1
        MOV     R3,#0
        MOV     R4,#0
        SWI     XOS_SetVarVal
        ADR     R0,xsysvar
        MOV     R1,R12
        MVN     R2,#0
        MOV     R3,#0
        MOV     R4,#0
        SWI     XOS_ReadVarVal
        TEQ     R2,#0
        CMNNE   R2,#1
        MOVNE   PC,R14
        MOV     R0,R11
        MOV     R1,R0
10
        LDRB    R2,[R0],#1
        TEQ     R2,#0
        BNE     %BT10
        SUB     R2,R0,R1
        ADR     R0,xsysvar
        MOV     R3,#0
        MOV     R4,#0
        SWI     XOS_SetVarVal
        MOV     PC,R14

xsysvar
        DCB     "X$Error", 0
rcsysvar
        DCB     "Sys$ReturnCode", 0
zero
        DCB     "0", 0
XVersionInfo
        DCB     "X 1.03 (03 Jul 2001)"
        ALIGN

        END

