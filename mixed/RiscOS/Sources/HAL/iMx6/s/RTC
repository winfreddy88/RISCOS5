; Copyright 2017 Castle Technology Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        $GetIO
        GET     Hdr:OSEntries
        GET     Hdr:HALEntries
        GET     Hdr:HALDevice
        GET     Hdr:RTCDevice
        GET     hdr:iMx6q
        GET     Hdr:iMx6qboard
        GET     hdr.StaticWS

        EXPORT  RTC_Init

        IMPORT  memcpy


        MACRO
        CallOS  $entry, $tailcall
        ASSERT  $entry <= HighestOSEntry
 [ "$tailcall"=""
        MOV     lr, pc
 |
   [ "$tailcall"<>"tailcall"
        ! 0, "Unrecognised parameter to CallOS"
   ]
 ]
        LDR     pc, OSentries + 4*$entry
        MEND

RTCAddressDAL   * &D0
                ^ 0
RTC_Public         # HALDevice_RTC_Size
RTCDeviceHAL_SB    # 4
RTCDevice_RTC_Size # 0

; HAL code for the DS1307 real time clock

        AREA    |Asm$$Code|, CODE, READONLY, PIC

RTC_Init ROUT
        Push    "lr"
        SUB     sp, sp, #12+12+8

        MOV     a1, sp
        MOV     a2, #RTCAddressDAL + 1  ; read op
        ADD     a3, sp, #12             ; -> data block
        MOV     a4, #1                  ; data block size
        STMIA   a1, {a2-a4}

        MOV     a2, #1                  ; 1 transfer
        ADD     a2, a2, #RTC_I2CNum<<24
        CallOS  OS_IICOpV
        TEQ     a1, #IICStatus_Completed
        BNE     %FT10                   ; some failure => no RTC device

        ADD     a1, sp, #12
        MOV     a2, #RTCAddressDAL + 1  ; read op
        ADD     a3, sp, #12+12+0        ; -> data block
 [ CuBox
        MOV     a4, #3                  ; data block size
 |
        MOV     a4, #1                  ; data block size
 ]
        STMIA   a1, {a2-a4}

        MOV     a2, #RTCAddressDAL
        ORR     a2, a2, #1:SHL:29       ; write op with retry
        ADD     a3, sp, #12+12+4        ; -> data block for write
        MOV     a4, #0
        STRB    a4, [a3]                ; start from register 0
        MOV     a4, #1                  ; data block size
        STMDB   a1!, {a2-a4}

        MOV     a2, #2                  ; 2 transfers
        ADD     a2, a2, #RTC_I2CNum<<24
        CallOS  OS_IICOpV

        LDR     a4, [sp, #12+12+0]      ; remember hi byte is reg start address
 [ CuBox
        TST     a4, #1:SHL:5            ; is the clock halted?
        TSTEQ   a4, #1:SHL:23           ; or possibly in powerdown
 |
        TST     a4, #1:SHL:7            ; is the clock halted?
 ]
        BEQ     %FT05

        MOV     a1, sp
        MOV     a2, #RTCAddressDAL
        ORR     a2, a2, #1:SHL:29       ; write op with retry
        ADD     a3, sp, #12             ; -> data block
 [ CuBox
        BIC     a4, a4, #1:SHL:5
        BIC     a4, a4, #1:SHL:23       ; hi order bit of PM bits
        BIC     a4, a4, #&ff:SHL:24
 |
        MOV     a4, #0
 ]
        STR     a4, [a3]                ; clear clock halt bit (and seconds)
 [ CuBox
        MOV     a4, #4                  ; data block size .. include PM bits
 |
        MOV     a4, #2                  ; data block size
 ]
        STMIA   a1, {a2-a4}

        MOV     a2, #1                  ; 1 transfer
        ADD     a2, a2, #RTC_I2CNum<<24
        CallOS  OS_IICOpV
05
        ADRL    a1, RTCDeviceStruct
        ADR     a2, RTCTemplate
        MOV     a3, #RTCDevice_RTC_Size
        BL      memcpy                  ; softcopy needed to append SB to

        STR     sb, [a1, #RTCDeviceHAL_SB]

        MOV     a2, a1                  ; device
        MOV     a1, #0                  ; flags
        CallOS  OS_AddDevice
10
        ADD     sp, sp, #12+12+8
        Pull    "pc"

RTCTemplate
        ; Public interface
        DCW     HALDeviceType_SysPeri + HALDeviceSysPeri_RTC
 [ CuBox
        DCW     HALDeviceID_RTC_PCF8523
 |
        DCW     HALDeviceID_RTC_DS1307
 ]
        DCD     HALDeviceBus_Ser + HALDeviceSerBus_IIC
        DCD     0                       ; API version
        DCD     RTCDesc
        DCD     0                       ; Address - N/A
        %       12                      ; Reserved
        DCD     RTCActivate
        DCD     RTCDeactivate
        DCD     RTCReset
        DCD     RTCSleep
        DCD     -1                      ; Interrupt N/A
        DCD     0
        %       8
        ; Specifics for an RTC
        DCB     RTCTimeFormat_BCD
        DCB     RTCFormatFlags_BCD_1BasedDay + \
                RTCFormatFlags_BCD_1BasedMonth + \
                RTCFormatFlags_BCD_NeedsYearHelp + \
                RTCFormatFlags_BCD_YearLOIsGood
        %       2
        DCD     RTCReadTime
        DCD     RTCWriteTime
        ASSERT  (.-RTCTemplate) = HALDevice_RTC_Size
        ; RTC's private data from here onwards
        DCD     0                       ; Copy of HAL's SB
        ASSERT  (.-RTCTemplate) = RTCDevice_RTC_Size
        ASSERT  ?RTCDeviceStruct = RTCDevice_RTC_Size

RTCDesc
 [ CuBox
        DCB     "PCF8523 real-time clock", 0
 |
        DCB     "DS1307 real-time clock", 0
 ]
        ALIGN

RTCActivate
        MOV     a1, #1
RTCDeactivate
RTCReset
        MOV     pc, lr
RTCSleep
        MOV     a1, #0                  ; Previously at full power
        MOV     pc, lr

        ; int RTCReadTime(struct rtcdevice *rtc, struct rtctime *time)
        ; Returns an RTCRetCode
RTCReadTime ROUT
        Push    "sb, v1, lr"

        MOV     v1, a2
        LDR     sb, [a1, #RTCDeviceHAL_SB]
        SUB     sp, sp, #12+12+8

        ADD     a1, sp, #12
        MOV     a2, #RTCAddressDAL + 1  ; read op
        ADD     a3, sp, #12+12+0        ; -> data block
        MOV     a4, #7                  ; data block size
        STMIA   a1, {a2-a4}

        MOV     a2, #RTCAddressDAL
        ORR     a2, a2, #1:SHL:29       ; write op with retry
        ADD     a3, sp, #12+12+7        ; -> data block
 [ CuBox
        MOV     a4, #3                  ; start from register 3
 |
        MOV     a4, #0                  ; start from register 0
 ]
        STRB    a4, [a3]                
        MOV     a4, #1                  ; data block size
        STMDB   a1!, {a2-a4}

        MOV     a2, #2                  ; 2 transfers
        ADD     a2, a2, #RTC_I2CNum<<24
        CallOS  OS_IICOpV
        TEQ     a1, #IICStatus_Completed
        MOVNE   a1, #RTCRetCode_Error
        BNE     %FT10

        ASSERT  RTCTimeStruct_BCD_Centiseconds = 0
        ASSERT  RTCTimeStruct_BCD_Seconds = 1
        ASSERT  RTCTimeStruct_BCD_Minutes = 2
        ASSERT  RTCTimeStruct_BCD_Hours = 3
    [ CuBox
        LDRB    a3, [sp, #12+12+3]      ; swap d-o-w and d-o-m order
        LDRB    a1, [sp, #12+12+4]
        STRB    a3, [sp, #12+12+4]
        STRB    a1, [sp, #12+12+3]
    ]
        LDR     a3, [sp, #12+12+0]
        MOV     a3, a3, LSL #8          ; zero centiseconds, discard d-o-w
        STR     a3, [v1, #0]

        ASSERT  RTCTimeStruct_BCD_DayOfMonth = 4
        ASSERT  RTCTimeStruct_BCD_Month = 5
        ASSERT  RTCTimeStruct_BCD_YearLO = 6
        LDR     a3, [sp, #12+12+4]
        BIC     a3, a3, #&FF:SHL:24     ; chip regs match struct
        STR     a3, [v1, #4]

        MOV     a1, #RTCRetCode_OK
10
        ADD     sp, sp, #12+12+8
        Pull    "sb, v1, pc"

        ; int RTCWriteTime(struct rtcdevice *rtc, const struct rtctime *time)
        ; Returns an RTCRetCode
RTCWriteTime
        Push    "sb, v1-v2, lr"

        ASSERT  RTCTimeStruct_BCD_Size = 8
        LDMIA   a2, {v1-v2}

        LDR     sb, [a1, #RTCDeviceHAL_SB]
        SUB     sp, sp, #12+12

        ADD     ip, sp, #12+3           ; -> data block

        CMP     v1, #-1
 [ CuBox
        MOVNE   a4, #3                  ; start from time register
        MOVEQ   a4, #6                  ; start from d-o-w register
 |
        MOVNE   a4, #0                  ; start from time register
        MOVEQ   a4, #4                  ; start from d-o-w register
 ]
        STRB    a4, [ip], #1
        ASSERT  RTCTimeStruct_BCD_Centiseconds = 0
        ASSERT  RTCTimeStruct_BCD_Seconds = 1
        ASSERT  RTCTimeStruct_BCD_Minutes = 2
        ASSERT  RTCTimeStruct_BCD_Hours = 3
        MOVNE   v1, v1, LSR #8          ; no cs
        ORRNE   v1, v1, #1:SHL:24
        STRNE   v1, [ip], #4            ; s,m,h,any d-o-w

        CMP     v2, #-1
        ASSERT  RTCTimeStruct_BCD_DayOfMonth = 4
        ASSERT  RTCTimeStruct_BCD_Month = 5
        ASSERT  RTCTimeStruct_BCD_YearLO = 6
        STRNE   v2, [ip], #3            ; dom,mon,ylo
    [ CuBox
        TEQ     a4, #3
        LDREQB  a3, [ip, #-4]          ; swap weekday and date order
        LDREQB  a1, [ip, #-3]          ; full string
        STREQB  a1, [ip, #-4]
        STREQB  a3, [ip, #-3]

        ADDNE   ip, ip, #1
        LDRNEB  a3, [ip, #-2]           ; reposition for chip order
        STRNEB  a3, [ip, #-1]     
        LDRNEB  a3, [ip, #-3]     
        STRNEB  a3, [ip, #-2]     
    ]

        MOV     a1, sp
        MOV     a2, #RTCAddressDAL
        ORR     a2, a2, #1:SHL:29       ; write op with retry
        ADD     a3, sp, #12+3           ; -> data block
        SUB     a4, ip, a3              ; data block size
        STMIA   a1, {a2-a4}

        MOV     a2, #1                  ; 1 transfer
        ADD     a2, a2, #RTC_I2CNum<<24
        CallOS  OS_IICOpV
        TEQ     a1, #IICStatus_Completed
        ASSERT  IICStatus_Completed = RTCRetCode_OK
        MOVNE   a1, #RTCRetCode_Error

        ADD     sp, sp, #12+12
        Pull    "sb, v1-v2, pc"

        END

