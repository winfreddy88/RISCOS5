; 
; Copyright (c) 1990, Neil Raine
; All rights reserved.
; 
; Redistribution and use in source and binary forms, with or without
; modification, are permitted provided that the following conditions are met: 
;     * Redistributions of source code must retain the above copyright
;       notice, this list of conditions and the following disclaimer.
;     * Redistributions in binary form must reproduce the above copyright
;       notice, this list of conditions and the following disclaimer in the
;       documentation and/or other materials provided with the distribution.
;     * Neither the name of Neil Raine nor the names of any contributors
;       may be used to endorse or promote products derived from this software
;       without specific prior written permission.
; 
; THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDER AND CONTRIBUTORS "AS IS"
; AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
; IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE
; ARE DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE
; LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
; CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
; SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
; INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
; CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
; ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
; POSSIBILITY OF SUCH DAMAGE.
; 
; > Sources.Macros

;;---------------------------------------------------------------------------
;; Macros
;;---------------------------------------------------------------------------

        MACRO
$lab    Move    $h,$v,$p
$lab    Push    "LR"
        Mv$h
        Mv$v
        Mv$p
        Pull    "PC"
        MEND

        MACRO
$lab    Mv
$lab
        MEND

        MACRO
$lab    MvL
$lab    MOVS    mask,mask,ROR bpp
        SUBCS   addr,addr,#4
        SUB     xco,xco,#1:SHL:velbits
        SUBS    temp,xco,x0
        ADDLT   xco,x1,temp
        BLLT    getaddr
        MEND

        MACRO
$lab    MvR
$lab    RSB     temp,bpp,#32
        MOV     temp,mask,ROR temp
        MOVS    mask,mask,LSL bpp
        MOVEQ   mask,temp
        ADDEQ   addr,addr,#4
        ADD     xco,xco,#1:SHL:velbits
        SUBS    temp,xco,x1
        ADDGE   xco,x0,temp
        BLGE    getaddr
        MEND

        MACRO
$lab    MvD
$lab    ADD     addr,addr,linelength
        SUB     yco,yco,#1:SHL:velbits
        SUBS    temp,yco,y0
        ADDLT   yco,y1,temp
        BLLT    getaddr
        MEND

        MACRO
$lab    MvU
$lab    SUB     addr,addr,linelength
        ADD     yco,yco,#1:SHL:velbits
        SUBS    temp,yco,y1
        ADDGE   yco,y0,temp
        BLGE    getaddr
        MEND

        MACRO
$lab    MvP
$lab    ADR     R14,grafx0
        LDMIA   R14,{x0,y0,x1,y1}       ; get grafx0,y0,x1,y1
        CMP     xco,x0
        CMPGE   x1,xco
        CMPGE   yco,y0
        CMPGE   y1,yco
        LDRGE   R14,colour
        ANDGE   R14,R14,mask
        LDRGE   temp,[addr]
        EORGE   temp,temp,R14
        STRGE   temp,[addr]
        ADR     R14,wrapx0
        LDMIA   R14,{x0,y0,x1,y1}       ; get wrapx0,y0,x1,y1
        MEND

        MACRO
$label  ALIGNHASH  $m
$label  #       ($m-1):AND:($m-(:INDEX:@):AND:($m-1))
        MEND

        MACRO
$label  FixDCB  $n,$string
        ASSERT  ((:LEN:"$string")<$n)
$label  DCB     "$string",13
        %       ($n-1-(:LEN:"$string"))
        MEND

;----------------------------------------------------------------------------

        END
