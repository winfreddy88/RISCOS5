; Copyright 2015 Castle Technology Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;


; s.module

        AREA    |main|, CODE, READONLY

        EXPORT |module|
        EXPORT |modDAhandler|
        EXPORT |moddata|
        EXPORT |moduleend|

        GET    VersionASM

Service_PreReset             * &45
Service_DynamicAreaRenumber  * &92
XOS_DynamicArea              * &20066
I_bit                        * &8000000 ;interrupt disable

module
        DCD     0               ; start entry
        DCD     0               ; init entry
        DCD     module_bye      - module
        DCD     module_service  - module
        DCD     module_title    - module
        DCD     module_helpstr  - module
        DCD     0               ; command table
        DCD     0               ; swi chunk
        DCD     0               ; swi code
        DCD     0               ; swi strings
        DCD     0               ; swi name decode code
        DCD     0               ; messages
        DCD     module_flags    - module

module_title
        DCB    "PSafeTest",0
        ALIGN
module_helpstr
        DCB    "PagesSafe Tester",9,Module_HelpVersion,0
        ALIGN

module_flags
        DCD     1 ; 32-bit OK

module_service
        TEQ     r1, #Service_PreReset
        TEQNE   r1, #Service_DynamicAreaRenumber
        MOVNE   pc, r14
        TEQ     r1, #Service_PreReset
        BEQ     serv_prereset
;serv_DArenum
        STR     r14,[r13,#-4]!
        LDR     r14,modDAN
        CMP     r14,#0
        LDREQ   pc,[r13],#4
        CMP     r14,r2
        STREQ   r3,modDAN
        LDR     pc,[r13],#4
serv_prereset
        STR     r14, [r13, #-4]!
;        BL    
        LDR     pc, [r13], #4

module_bye
        STMFD   r13!, {r0,r1,r14}
        MOV     r0,#1
        LDR     r1,modDAN
        CMP     r1,#0
        SWINE   XOS_DynamicArea       ;remove dynamic area
        MOV     r1,#0
        STR     r1,modDAN             ;mark as removed
        LDMFD   r13!, {r0,r1,pc}

modDAhandler
        CMP     r0,#2           ;preshrink?
        BEQ     mdah_preshrink
        CMP     r0,#0           ;pregrow?  (NB clears V)
        MOVNE   pc,r14
;pregrow
        STMFD   r13!,{r0-r3}
        ADR     r3,modnextpageneeded
pregrowloop
        LDR     r0,[r3],#4
        STR     r0,[r1],#12 
        SUBS    r2,r2,#1
        BNE     pregrowloop
        LDMFD   r13!,{r0-r3}
        MOV     pc,r14
;
mdah_preshrink
        MOV     pc,r14


;values will be poked in by C (after module is inserted, and copied to RMA)
moddata
modnextpageneeded            ;next page numbers (up to 8) required for DA on next grow
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0
        DCD     0
modDAN
        DCD     0            ;dynamic area number

moduleend
        DCD     0

        END
