FX 247 2_10101010
FX 229 1

If "<Run$Path>" = ",%." Then Set Run$Path <Boot$Dir>.Library.,,%.
If "<Run$Path>" = "Resources:$.Library.,%.," Then Set Run$Path Resources:$.Library.,<Boot$Dir>.Library.,,%.
 
RMEnsure BootCommands 1.30 RMLoad System:Modules.BootCmds
RMEnsure BootCommands 1.30 Error This !Boot structure requires BootCommands version 1.30 or later
Set Boot$ProgressLoad -progress 0,50
Set Boot$ProgressRun  -progress 50,50
RMEnsure BootCommands 1.44 Unset Boot$Progress*

If Boot$OSVersion < 350 Then RMEnsure CMOSUtils  0.00 RMLoad System:Modules.CMOSUtils
If Boot$OSVersion < 500 Then RMEnsure AppPatcher 0.00 X RMLoad System:Modules.PatchApp

IfThere <Boot$Dir>.^.!Territory Then /<Boot$Dir>.^.!Territory

If Boot$OSVersion >= 350 Then WimpSlot -next 256K
If Boot$OSVersion >= 350 Then FreePool
AddToRMA 768K

Set Alias$BootLoad Echo %%*0|mLoad %%*0|mEcho |<11>|<23>|<8>|<5>|<6>|<0>|<0>|<0>|<0>|<0>|<0>|<11>|m
Set Alias$BootRun  Echo %%*0|mRun %%*0|mEcho |<11>|<23>|<8>|<5>|<6>|<0>|<0>|<0>|<0>|<0>|<0>|<11>|m
Set Alias$BootObey Obey -c %%*0
Set Alias$@LoadType_FEB BootObey %%*0
Set Alias$@LoadType_FF2 Error CMOS file found within !Boot (%%0)
Set Alias$@LoadType_FF8 /%%*0
Set Alias$@LoadType_FF9 IconSprites %%*0
Set Alias$@LoadType_FFA RMLoad %%*0
Set Alias$@LoadType_FFB BASIC -quit %%*0
Set Alias$@LoadType_FFC /%%*0

/<Boot$Dir>.Utils.HOn

/<Boot$Dir>.Utils.SetChoices
BootObey Choices:Boot.PreDesktop

If "<Wimp$ScrapDir>" = "" Then IfThere <BootResources$Dir>.!Scrap Then /<BootResources$Dir>.!Scrap

IfThere <BootResources$Dir>.!Internet Then /<BootResources$Dir>.!Internet.!Boot
If ("<InetDBase$Path>" <> "") AND ("<Inet$MimeMappings>" = "") Then Set Inet$MimeMappings InetDBase:MimeMap
RMEnsure MimeMap 0.00 X RMReinit MimeMap
RMEnsure MimeMap 0.00 X RMLoad System:Modules.Network.MimeMap

Do Repeat BootLoad <Boot$ToBeLoaded> -files <Boot$ProgressLoad>
Do Repeat BootRun  <Boot$ToBeLoaded> -directories <Boot$ProgressRun>

Unset Alias$@LoadType_FEB
Unset Alias$@LoadType_FF8
Set   Alias$@LoadType_FF9 SLoad %%0
Set   Alias$@LoadType_FFA RMLoad %%*0
Set   Alias$@LoadType_FFB Basic -load |"%%0|" %%*1
Unset Alias$@LoadType_FFC
Unset Alias$BootLoad
Unset Alias$BootRun
Unset Alias$BootObey
Unset Boot$Remote
Unset Boot$Progress*

AppSize 512000K

FX 247 1
FX 229 0
/Choices:Boot.Desktop
