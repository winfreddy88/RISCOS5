; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Proc
        GET     Hdr:ModHand
        GET     Hdr:ResourceFS
        GET     Hdr:HighFSI

        GBLL    Debug
Debug   SETL    {FALSE}

      [ Debug
        GET     Hdr:Debug
Host_Debug      SETL {FALSE}
Debug_MaybeIRQ  SETL {FALSE}
      ]

        AREA    IfThere, CODE, READONLY

Start
        MOV     r0, #OSFile_ReadNoPath
        SWI     XOS_File
        SUBVSS  r0, r0, r0      ; CLRV and set r0 to 0 - behave as if file not there
        TEQ     r0, #0
        LDREQ   r0, If0
        LDRNE   r0, If1
        MOV     r2, r12
        STR     r0, [r2], #4
SkipFileName
        LDRB    r0, [r1], #1
        CMP     r0, #" "
        BHI     SkipFileName
        B       IntoCopyLoop
StartCopyLoop
        LDRB    r0, [r1], #1
        CMP     r0, #" "
IntoCopyLoop
        STRB    r0, [r2], #1
        BHS     StartCopyLoop
        MOV     r0, r12
        SWI     XOS_CLI
        MOV     pc, lr

If0     DCB     "If 0"
If1     DCB     "If 1"

 [ Debug
        InsertDebugRoutines
 ]

        DCB     "IfThere 1.01 13-Feb-2003 © Acorn Computers Ltd 1993", 0
        ALIGN

        END
