; Copyright 1997 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
	GET	Hdr:ListOpts
	GET	Hdr:Macros
	GET	Hdr:APCS.<APCS>
	GET	Hdr:System
	GET	Hdr:Services
	GET	Hdr:Toolbox

	AREA	|SWI$$Code|, READONLY, CODE, PIC

;
; Routines too specialist for OSLib 6.9
;

        EXPORT  xservice_enumerate_network_drivers
xservice_enumerate_network_drivers
        MOV     R2,R0
        MOV     R3,R1
        MOV     R0,#0                   ; NULL chain to start
        MOV     R1,#Service_EnumerateNetworkDrivers
        SWI     XOS_ServiceCall
        Return  ,LinkNotStacked,VS
        TEQ     R2,#0
        STRNE   R0,[R2]                 ; chain
        TEQ     R3,#0
        STRNE   R1,[R3]                 ; unclaimed
        MOV     R0,#0                   ; no error
        Return  ,LinkNotStacked

        EXPORT  service_enumerate_network_drivers
service_enumerate_network_drivers
        MOV     R2,R0
        MOV     R0,#0                   ; NULL chain to start
        MOV     R1,#Service_EnumerateNetworkDrivers
        SWI     OS_ServiceCall
        TEQ     R2,#0
        STRNE   R0,[R2]                 ; chain
        MOV     R0,R1                   ; unclaimed
        Return  ,LinkNotStacked

        EXPORT  network_driver_return_stats
        EXPORT  network_driver_implemented_stats
network_driver_return_stats
        MOV     R3, #1                  ; return the statistics
        B       %FT10
network_driver_implemented_stats
        MOV     R3, #0                  ; return which statistics are implemented
10
        ADD     R12,R0,#6               ; the stats SWI offset from dib_swibase
        MOV     R0,R3
        SWI     OS_CallASWIR12
        Return  ,LinkNotStacked

        EXPORT  xnetwork_driver_return_stats
        EXPORT  xnetwork_driver_implemented_stats
xnetwork_driver_return_stats
        MOV     R3, #1                  ; return the statistics
        B       %FT10
xnetwork_driver_implemented_stats
        MOV     R3, #0                  ; return which statistics are implemented
10
        ADD     R12,R0,#6               ; the stats SWI offset from dib_swibase
        MOV     R0,R3
        SWI     XOS_CallASWIR12
        MOVVC   R0,#0                   ; no error
        Return  ,LinkNotStacked

;
; Routines to provide OSLib 5.1 functionality when linked with OSLib 4.8
;

RadioButton_GetState		*       389

        EXPORT  my_radiobutton_get_state
my_radiobutton_get_state
        Push    "R3"
        MOV     R3,R2
        LDR     R2,=RadioButton_GetState
        SWI     Toolbox_ObjectMiscOp
        Pull    "R3"
        TEQ     R3,#0
        STRNE   R1,[R3]
        Return	,LinkNotStacked

	END
