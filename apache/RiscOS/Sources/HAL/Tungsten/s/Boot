; Copyright 2002 Tematic Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:System
        GET     Hdr:Machine.<Machine>
        GET     Hdr:HALSize.<HALSize>

        GET     Hdr:MEMM.ARM600

        GET     Hdr:Proc
        GET     Hdr:OSEntries
        GET     Hdr:HALEntries

        GET     hdr.StaticWS
        GET     hdr.80321

; This version assumes a RISC OS image starting OSROM_HALSize bytes after us.

        AREA    |Asm$$Code|, CODE, READONLY, PIC

        EXPORT  get_end_of_ram
        EXPORT  rom_checkedout_ok

        IMPORT  HAL_Base
        IMPORT  generate_POR_flags
        IMPORT  HAL_PCIWriteConfigHalfword
        IMPORT  PCI_TungstenSecondaryBus

; v8 is used as pointer to RISC OS entry table throughout pre-MMU stage.
        MACRO
        CallOSM $entry, $reg
        LDR     ip, [v8, #$entry*4]
        MOV     lr, pc
        ADD     pc, v8, ip
        MEND

rom_checkedout_ok

 [ RAM_Base <> Boot_RAM_Base

        SUB     a3, a4, #ROM_Base
        SUB     pc, pc, a3
        NOP

        MCR     p15, 0, a1, c7, c5      ; invalidate I cache again

; now move the RAM into final position

        STMFD   sp!, {a1-a4}
        BL      move_ram
        LDMFD   sp!, {a1-a4}

; and back into RAM, at final position
        ADD     a3, a3, #RAM_Base - Boot_RAM_Base
        ADD     a4, a4, #RAM_Base - Boot_RAM_Base
        ADD     v8, v8, #RAM_Base - Boot_RAM_Base
        ADD     pc, pc, a3
        NOP
 ]

        LDMFD   sp!, {v1-v3}                    ; junk stack contents from copy_rom_to_ram

        MOV     sb, #0

        BL      get_end_of_ram
        MOV     a3, a1

        MOV     a1, #4:SHL:8
        LDR     a2, =RAM_Base
        ADRL    lr, HAL_Base                    ; bottom of HAL, may be top of RAM
        CMP     lr, a3
        MOVLO   a3, lr
        LDR     a4, =&FFFFFFFF
        MOV     ip, #0
        STR     ip, [sp, #-4]!
        CallOSM OS_AddRAM
        STR     a1, [sp]
        ADD     sp, sp, #4

        MOV     v1, a1
        BL      generate_POR_flags
        ORR     a1, a1, #OSStartFlag_RAMCleared

        ADRL    a2, HAL_Base + OSROM_HALSize          ; a2 -> RISC OS image
        ADR     a3, HALdescriptor
        MOV     a4, v1
        CallOSM OS_Start


HALdescriptor   DATA
        DCD     HALFlag_NCNBWorkspace
        DCD     HAL_Base - HALdescriptor
        DCD     OSROM_HALSize
        DCD     HAL_EntryTable - HALdescriptor
        DCD     HAL_Entries
        DCD     HAL_WsSize


        MACRO
        HALEntry $name
        ASSERT  (. - HAL_EntryTable) / 4 = EntryNo_$name
        DCD     $name - HAL_EntryTable
        MEND

        MACRO
        NullEntry
        DCD     HAL_Null - HAL_EntryTable
        MEND

        IMPORT   Video_init
        IMPORT   Interrupt_Init
        IMPORT   Timer_Init
        IMPORT   HAL_DebugInit
        IMPORT   HAL_DebugInit2
        IMPORT   PCI_Init
        IMPORT   IDE_Init
        IMPORT   IDEDevice_Init
        IMPORT   M1535DMA_Init
        IMPORT   Audio_Init
        IMPORT   RTC_Init

        IMPORT   HAL_IRQEnable
        IMPORT   HAL_IRQDisable
        IMPORT   HAL_IRQSource
        IMPORT   HAL_IRQStatus
        IMPORT   HAL_FIQEnable
        IMPORT   HAL_FIQDisable
        IMPORT   HAL_FIQDisableAll
        IMPORT   HAL_FIQSource
        IMPORT   HAL_FIQStatus
        IMPORT   HAL_IRQMax

        IMPORT   HAL_Timers
        IMPORT   HAL_TimerDevice
        IMPORT   HAL_TimerGranularity
        IMPORT   HAL_TimerMaxPeriod
        IMPORT   HAL_TimerSetPeriod
        IMPORT   HAL_TimerPeriod
        IMPORT   HAL_TimerReadCountdown
        IMPORT   HAL_TimerIRQClear

        IMPORT   HAL_CounterRate
        IMPORT   HAL_CounterPeriod
        IMPORT   HAL_CounterRead
        IMPORT   HAL_CounterDelay

        IMPORT   HAL_IICBuses
        IMPORT   HAL_IICType
        IMPORT   HAL_IICDevice
        IMPORT   HAL_IICTransfer
        IMPORT   HAL_IICMonitorTransfer

        IMPORT   HAL_NVMemoryType
        IMPORT   HAL_NVMemorySize
        IMPORT   HAL_NVMemoryPageSize
        IMPORT   HAL_NVMemoryProtectedSize
        IMPORT   HAL_NVMemoryProtection
        IMPORT   HAL_NVMemoryIICAddress

        IMPORT   HAL_UARTPorts
        IMPORT   HAL_UARTStartUp
        IMPORT   HAL_UARTShutdown
        IMPORT   HAL_UARTFeatures
        IMPORT   HAL_UARTReceiveByte
        IMPORT   HAL_UARTTransmitByte
        IMPORT   HAL_UARTLineStatus
        IMPORT   HAL_UARTInterruptEnable
        IMPORT   HAL_UARTRate
        IMPORT   HAL_UARTFormat
        IMPORT   HAL_UARTFIFOSize
        IMPORT   HAL_UARTFIFOClear
        IMPORT   HAL_UARTFIFOEnable
        IMPORT   HAL_UARTFIFOThreshold
        IMPORT   HAL_UARTInterruptID
        IMPORT   HAL_UARTBreak
        IMPORT   HAL_UARTModemControl
        IMPORT   HAL_UARTModemStatus
        IMPORT   HAL_UARTDevice
        IMPORT   HAL_UARTDefault

        IMPORT   HAL_MachineID

        IMPORT   HAL_DebugRX
        IMPORT   HAL_DebugTX

        IMPORT   HAL_PCIFeatures
        IMPORT   HAL_PCIReadConfigByte
        IMPORT   HAL_PCIReadConfigHalfword
        IMPORT   HAL_PCIReadConfigWord
        IMPORT   HAL_PCIWriteConfigByte
        IMPORT   HAL_PCIWriteConfigHalfword
        IMPORT   HAL_PCIWriteConfigWord
;       IMPORT   HAL_PCISpecialCycle
        IMPORT   HAL_PCISlotTable
        IMPORT   HAL_PCIAddresses

        IMPORT   HAL_KbdScanDependencies

HAL_EntryTable  DATA
        HALEntry HAL_Init

        HALEntry HAL_IRQEnable
        HALEntry HAL_IRQDisable
        NullEntry ; HALEntry HAL_IRQClear
        HALEntry HAL_IRQSource
        HALEntry HAL_IRQStatus
        HALEntry HAL_FIQEnable
        HALEntry HAL_FIQDisable
        HALEntry HAL_FIQDisableAll
        NullEntry ; HALEntry HAL_FIQClear
        HALEntry HAL_FIQSource
        HALEntry HAL_FIQStatus

        HALEntry HAL_Timers
        HALEntry HAL_TimerDevice
        HALEntry HAL_TimerGranularity
        HALEntry HAL_TimerMaxPeriod
        HALEntry HAL_TimerSetPeriod
        HALEntry HAL_TimerPeriod
        HALEntry HAL_TimerReadCountdown

        HALEntry HAL_CounterRate
        HALEntry HAL_CounterPeriod
        HALEntry HAL_CounterRead
        HALEntry HAL_CounterDelay

        HALEntry HAL_NVMemoryType
        HALEntry HAL_NVMemorySize
        HALEntry HAL_NVMemoryPageSize
        HALEntry HAL_NVMemoryProtectedSize
        HALEntry HAL_NVMemoryProtection
        HALEntry HAL_NVMemoryIICAddress
        NullEntry ; HAL_NVMemoryRead 
        NullEntry ; HAL_NVMemoryWrite

        HALEntry HAL_IICBuses
        HALEntry HAL_IICType
        NullEntry ; HAL_IICSetLines   
        NullEntry ; HAL_IICReadLines  
        HALEntry HAL_IICDevice
        HALEntry HAL_IICTransfer
        HALEntry HAL_IICMonitorTransfer

        NullEntry ; HALEntry HAL_VideoFlybackDevice
        NullEntry ; HALEntry HAL_VideoSetMode
        NullEntry ; HALEntry HAL_VideoWritePaletteEntry
        NullEntry ; HALEntry HAL_VideoWritePaletteEntries
        NullEntry ; HALEntry HAL_VideoReadPaletteEntry
        NullEntry ; HALEntry HAL_VideoSetInterlace
        NullEntry ; HALEntry HAL_VideoSetBlank
        NullEntry ; HALEntry HAL_VideoSetPowerSave
        NullEntry ; HALEntry HAL_VideoUpdatePointer
        NullEntry ; HALEntry HAL_VideoSetDAG
        NullEntry ; HALEntry HAL_VideoVetMode
        NullEntry ; HALEntry HAL_VideoPixelFormats
        NullEntry ; HALEntry HAL_VideoFeatures
        NullEntry ; HALEntry HAL_VideoBufferAlignment
        NullEntry ; HALEntry HAL_VideoOutputFormat

        NullEntry ; HALEntry HAL_MatrixColumns
        NullEntry ; HALEntry HAL_MatrixScan

        NullEntry ; HALEntry HAL_TouchscreenType
        NullEntry ; HALEntry HAL_TouchscreenRead
        NullEntry ; HALEntry HAL_TouchscreenMode
        NullEntry ; HALEntry HAL_TouchscreenMeasure

        HALEntry HAL_MachineID

        HALEntry HAL_ControllerAddress
        HALEntry HAL_HardwareInfo
        HALEntry HAL_SuperIOInfo
        HALEntry HAL_PlatformInfo
        NullEntry ; HALEntry HAL_CleanerSpace

        HALEntry HAL_UARTPorts
        HALEntry HAL_UARTStartUp
        HALEntry HAL_UARTShutdown
        HALEntry HAL_UARTFeatures
        HALEntry HAL_UARTReceiveByte
        HALEntry HAL_UARTTransmitByte
        HALEntry HAL_UARTLineStatus
        HALEntry HAL_UARTInterruptEnable
        HALEntry HAL_UARTRate
        HALEntry HAL_UARTFormat
        HALEntry HAL_UARTFIFOSize
        HALEntry HAL_UARTFIFOClear
        HALEntry HAL_UARTFIFOEnable
        HALEntry HAL_UARTFIFOThreshold
        HALEntry HAL_UARTInterruptID
        HALEntry HAL_UARTBreak
        HALEntry HAL_UARTModemControl
        HALEntry HAL_UARTModemStatus
        HALEntry HAL_UARTDevice
        HALEntry HAL_UARTDefault

        HALEntry HAL_DebugRX
        HALEntry HAL_DebugTX

        HALEntry HAL_PCIFeatures
        HALEntry HAL_PCIReadConfigByte
        HALEntry HAL_PCIReadConfigHalfword
        HALEntry HAL_PCIReadConfigWord
        HALEntry HAL_PCIWriteConfigByte
        HALEntry HAL_PCIWriteConfigHalfword
        HALEntry HAL_PCIWriteConfigWord
        NullEntry ; HALEntry HAL_PCISpecialCycle
        HALEntry HAL_PCISlotTable
        HALEntry HAL_PCIAddresses

        HALEntry HAL_PlatformName
        NullEntry ; Unused
        NullEntry ; Unused

        HALEntry HAL_InitDevices

        HALEntry HAL_KbdScanDependencies
        NullEntry ; Unused
        NullEntry ; Unused
        NullEntry ; Unused

        HALEntry HAL_PhysInfo

        HALEntry HAL_Reset

        HALEntry HAL_IRQMax

        NullEntry ; HALEntry HAL_USBControllerInfo
        NullEntry ; HALEntry HAL_USBPortPower
        NullEntry ; HALEntry HAL_USBPortIRQStatus
        NullEntry ; HALEntry HAL_USBPortIRQClear
        NullEntry ; HALEntry HAL_USBPortDevice

        HALEntry HAL_TimerIRQClear


HAL_Entries     * (.-HAL_EntryTable)/4


;--------------------------------------------------------------------------------------


HAL_Init
        Entry

        STR     a2, NCNBWorkspace

        BL      SetUpOSEntries

        MOV     a1, #0
        LDR     a2, =IO_Base
        MOV     a3, #IO_Size
        CallOS  OS_MapInIO
        STR     a1, IO_Address
        ADD     a1, a1, #&00200000    ; Offset to base of IOMD
        STR     a1, IOMD_Address

        MOV     a1, #0
        LDR     a2, =PMMR_Base
        LDR     a3, =PMMR_Size
        CallOS  OS_MapInIO
        ADD     a1, a1, #PMMR - PMMR_Base
        STR     a1, PMMR_Address

        MOV     a1, #0
        LDR     a2, =EASI_Base
        MOV     a3, #EASI_Size
        CallOS  OS_MapInIO
        STR     a1, EASI_Address

        MOV     a1, #0
        LDR     a2, =PCI_IO_Base
        MOV     a3, #PCI_IO_Size
        CallOS  OS_MapInIO
        STR     a1, PCI_IO_Address

        BL      Timer_Init

        BL      HAL_DebugInit

        BL      PCI_Init

        BL      HAL_DebugInit2

        BL      Interrupt_Init

        BL      IDE_Init

   [ :LNOT: (Debug :LAND: (DebugPort = 3))
        MOV     a1,#0
        BL      HAL_UARTStartUp
   ]
;        MOV     a1,#1
;        BL      HAL_UARTStartUp
;        MOV     a1,#2
;        BL      HAL_UARTStartUp

        EXIT

; Initialise and relocate the entry table.
SetUpOSEntries  ROUT
        STR     a1, OSheader
        LDR     a2, [a1, #OSHdr_NumEntries]
        CMP     a2, #HighestOSEntry+1
        MOVHI   a2, #HighestOSEntry+1

        ADR     a3, OSentries
        LDR     a4, [a1, #OSHdr_Entries]
        ADD     a4, a4, a1

05      SUBS    a2, a2, #1
        LDR     ip, [a4, a2, LSL #2]
        ADD     ip, ip, a4
        STR     ip, [a3, a2, LSL #2]
        BNE     %BT05
        ; Fall through

HAL_Null
        MOV     pc, lr

HAL_InitDevices
        Entry

        BL      RTC_Init
        
        BL      M1535DMA_Init

        BL      Audio_Init

        BL      Video_init

        BL      IDEDevice_Init

        EXIT

HAL_ControllerAddress
        AND     a3, a2, #&FF
        MOV     a2, a2, LSR#8                   ; a3=sequence number a2=controller
        CMP     a2, #6
        MOVCS   a1, #-1                         ; don't know about that controller
        MOVCS   pc, lr

        TEQ     a2, #0
        LDREQ   a1, IOMD_Address
        ADDEQ   a1, a1, #ECTCR
        MOVEQ   pc, lr

        TEQ     a2, #1
        BNE     %FT15
        CMP     a3, #NumberOfPodules
        MOVCS   a1, #0
        LDRCC   a1, EASI_Address
        ADDCC   a1, a1, a3, LSL#24
        MOV     pc, lr
15
        TEQ     a2, #4
        LDREQ   a1, IOMD_Address
        MOVNE   a1, #0                          ; one we know about,but isn't fitted
        MOV     pc, lr

HAL_HardwareInfo
        LDR     ip, =&FFFFFF00
        STR     ip, [a1]
        MOV     ip, #0
        STR     ip, [a2]
        STR     ip, [a3]
        MOV     pc, lr

HAL_PlatformInfo
 [ RevB
        MOV     ip, #2_11011                    ; podules,PCI cards,soft off,and soft ROM
 |
        MOV     ip, #2_10011                    ; podules,PCI cards,soft off didn't work,and soft ROM
 ]
        STR     ip, [a2]
        MOV     ip, #2_11111                    ; mask of valid bits
        STR     ip, [a3]
        MOV     pc, lr

HAL_PlatformName
        ADR     a1, %FT01
        MOV     pc, lr
01      DCB     "Tungsten", 0
        ALIGN
                        
HAL_SuperIOInfo
        MOV     ip, #0
        STR     ip, [a1]
        STR     ip, [a2]
        MOV     pc, lr

; Shifts to determine number of bytes/words to allocate in table.
NibbleShift     *       12
ByteShift       *       NibbleShift + 1
WordShift       *       ByteShift + 2

; Bit patterns for different types of memory.
NotPresent      *       &00000000
DRAM_Pattern    *       &11111111
VRAM_Pattern    *       &22222222
ROM_Pattern     *       &33333333
IO_Pattern      *       &44444444
NotAvailable    *       &88888888

        IMPORT  memset

HAL_PhysInfo
        TEQ     a1, #PhysInfo_GetTableSize
        MOVEQ   a1, #1:SHL:(32-ByteShift)
        STREQ   a1, [a2]
        MVNEQ   a1, #0             ; Supported
        MOVEQ   pc, lr

        TEQ     a1, #PhysInfo_HardROM
        MOVEQ   a1, #&A0000000     ; 4MB of NOR flash here
        LDREQ   a2, =&A0400000-1   ; Inclusive end address
        STMEQIA a3, {a1-a2}
        MVNEQ   a1, #0             ; Supported
        MOVEQ   pc, lr

        TEQ     a1, #PhysInfo_WriteTable
        MOVNE   a1, #0
        MOVNE   pc, lr

        ; Do the PhysInfo_WriteTable table output 
        Push    "v1-v2,lr"
        MOV     a1, #&C0000000     ; Physical RAM
        LDR     lr, =&FFFFE000-1
        STMIA   a3, {a1,lr}
        MOV     v1, a2

        ADR     v2, HAL_PhysTable
10      LDMIA   v2, {a1, a2, lr}
        SUB     a3, lr, a1
        ADD     a1, v1, a1, LSR #ByteShift
        MOV     a3, a3, LSR #ByteShift
        BL      memset
        LDR     a1, [v2, #8]!
        TEQ     a1, #0
        BNE     %BT10

        MVN     a1, #0             ; Supported
        Pull    "v1,v2,pc"

; HAL_PhysInfo uses memset to fill the table, so all regions
; must be byte-aligned (ie double-page-aligned addresses).
HAL_PhysTable
        DCD     &00000000, IO_Pattern  :OR: NotAvailable ; PCI memory windows
        DCD     &88000000, NotPresent  :OR: NotAvailable
        DCD     &90000000, IO_Pattern  :OR: NotAvailable ; PCI I/O window
        DCD     &90010000, NotPresent  :OR: NotAvailable
        DCD     &A0000000, ROM_Pattern :OR: NotAvailable ; 4MB of ROM
        DCD     &A0400000, NotPresent  :OR: NotAvailable
        DCD     &A3000000, IO_Pattern  :OR: NotAvailable ; IOMD + podules
        DCD     &A3400000, NotPresent  :OR: NotAvailable
        DCD     &A8000000, IO_Pattern  :OR: NotAvailable ; EASI space for podules
        DCD     &AC000000, NotPresent  :OR: NotAvailable
        DCD     &FFFFE000, IO_Pattern  :OR: NotAvailable ; PMMR
        DCD     0

HAL_Reset
        TEQ     a1, #0                      ; reset,or power off?
        LDRNE   ip, PMMR_Address
        LDRNE   a1, [ip, #ATU_PCSR]
        ORRNE   a1, a1, #1:SHL:5
        STRNE   a1, [ip, #ATU_PCSR]
        BNE     .

        BL      PCI_TungstenSecondaryBus
        MOV     v1, a1
        MOV     a2, #12*8
        MOV     a3, #&9A                    ; a1 = bus, a2 = dev/fn, a3 = addr
        MOV     a4, #4
        BL      HAL_PCIWriteConfigHalfword  ; GPIO2 is output
        MOV     a1, v1
        MOV     a2, #12*8
        MOV     a3, #&9C
        BL      HAL_PCIWriteConfigHalfword  ; Bit 2 high in PMU device
        B       .

        LTORG

        EXPORT  vtophys
vtophys
        CallOS  OS_LogToPhys, tailcall

        EXPORT  mapinio
mapinio
        CallOS  OS_MapInIO, tailcall

move_ram
 [ RAM_Base <> Boot_RAM_Base
        ; so that SBR regs don't need changing
        ASSERT  (RAM_Base :AND: &3FFFFFFF) = (Boot_RAM_Base :AND: &3FFFFFFF)
        LDR     ip, =PMMR
        MOV     a1, #RAM_Base
        STR     a1, [ip, #MCU_SDBR]
        ADD     sp, sp, #RAM_Base - Boot_RAM_Base
 ]
        MOV     pc, lr

get_end_of_ram
        LDR     ip, =PMMR
        LDR     a1, [ip, #MCU_SBR1]             ; read end direct from control register
        LDR     a2, [ip, #MCU_SDBR]             ; and start
        AND     a1, a1, #&3F
        AND     a2, a2, #&C0000000
        ADD     a1, a2, a1, LSL #25
        SUB     a3, a1, #1
        CMP     a3, #&FFF00000
        MOVHS   a1, #&FFF00000-1
        ADDHS   a1, a1, #1
        MOV     pc, lr

        END
