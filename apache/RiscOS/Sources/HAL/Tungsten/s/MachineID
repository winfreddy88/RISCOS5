; Copyright 2002 Tematic Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:OSEntries
        GET     Hdr:HALEntries

        GET     hdr.StaticWS
        GET     hdr.80321

        AREA    |Asm$$Code|, CODE, READONLY, PIC
        EXPORT  HAL_MachineID

HAL_MachineID
        ; the Unique id is stored in an 8 byte hidden region of flash which is
        ; either programmed by hand,or at the factory in batches.The format is thus:
        ; 0x81 aa bb cc dd ee ff CC where aa-ff is the MAC address and CC is a crc
        ; calculated using the same crc algorithm as the DS2401 used
        Push    "v1-v2,lr"
        MOV     a1, #0
        LDR     a2, =ROM_Base
        MOV     a3, #16              ; will get rounded up to 1MByte!
        CallOS  OS_MapInIO           ; a1=logical memory of bottom of ROM

        ADD     a4, a1, #&055*2
        ADD     a4, a4, #&500*2      ; v4 -> &555
        ADD     a3, a1, #&0AA*2
        ADD     a3, a3, #&200*2      ; v3 -> &2AA
        MOV     v2, #&55             ; v2 = &55
        MOV     v1, #&AA             ; v1 = &AA

        STRH    v1, [a4]
        STRH    v2, [a3]
        MOV     ip, #&88
        STRH    ip, [a4]             ; page in hidden flash area

        LDMIA   a1, {a1, a2}

        ; the id area may be blank,leading to an id of 0xFFFFFFFF but that'll fail
        ; the crc check and be set to zero so no need to check it here

        STRH    v1, [a4]
        STRH    v2, [a3]
        MOV     ip, #&90
        STRH    ip, [a4]
        MOV     ip, #0
        STRH    ip, [a4]             ; page out hidden area
        Pull    "v1-v2,pc"           ; don't need the flash anymore,but looks like I'm stuck with it

        END
