REM >!Flasher.!RunImage

REM Copyright 2015 Castle Technology Ltd
REM
REM Licensed under the Apache License, Version 2.0 (the "License");
REM you may not use this file except in compliance with the License.
REM You may obtain a copy of the License at
REM
REM     http://www.apache.org/licenses/LICENSE-2.0
REM
REM Unless required by applicable law or agreed to in writing, software
REM distributed under the License is distributed on an "AS IS" BASIS,
REM WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
REM See the License for the specific language governing permissions and
REM limitations under the License.
REM
REM  By Merlyn Kline 1990

ONERROR:PROCerror(ERR,REPORT$,ERL,TRUE)
PROCinit
ONERROR:PROCerror(ERR,REPORT$,ERL,FALSE)

REPEAT
SYS "Wimp_PollIdle",%100110000,q%,delay% TO reason%
PROCrespond(reason%)
UNTILquit%
PROCquit
END

DEFPROCrespond(reason%)
CASEreason%OF
WHEN 0:PROCbackgnd
WHEN 1:
WHEN 2:SYS "Wimp_OpenWindow",,q%
WHEN 3:PROCclose_window(!q%)
WHEN 4:
WHEN 5:
WHEN 6:PROCmouse(!q%,q%!4,q%!8,q%!12,q%!16,q%!20)
WHEN 7
WHEN 8:
WHEN 9:PROCmenuselect
WHEN10
WHEN11
WHEN12
WHEN17,18:PROCumess1
ENDCASE
ENDPROC

DEFPROCquit
PROCrestorec
$q%="TASK":SYS "Wimp_CloseDown",taskhandle%,!q%
PROCclose_messages
END
ENDPROC

DEFPROCinit
DIM val% 100
application$=""
lastparam$=""
qsize%=1000
DIM q% qsize%,workbuf% 400
PROCopen_messages("<Flasher$Dir>.Messages")
appl$=FNlookup("Name")
version$=FNlookup("_Version")
IFFNinitwimp<200ERROR99,FNlookup("E00")
*fx229,1
quit%=FALSE
delay%=0
flashing%=TRUE:findcaret%=FALSE
offtime%=20
ontime%=45
autoscroll%=TRUE
DIM template% 400
PROCset_templates
PROCdefine_menus
PROCversion
PROCbaricon
ENDPROC

DEFPROCversion
LOCALT$:T$=version$
PROCset_icontext(w_pinfo%,4,T$)
ENDPROC

DEFFNprod=MID$(version$,INSTR(version$,".")+1,1)="0"

DEFFNdebug=NOTFNprod ANDINKEY-1ANDINKEY-2

DEFFNinitwimp
$q%="TASK":SYS "Wimp_Initialise",200,!q%,appl$ TO !q%,taskhandle%
=!q%

DEFPROCbaricon
!q%=-1:q%!4=-0:q%!8=0:q%!12=64:q%!16=64
q%!20=%11000000000010
$(q%+24)="!"+appl$
SYS"Wimp_CreateIcon",,q%TObaricon%
ENDPROC

DEFPROCerror(err%,err$,erl%,quit%)
@%=&90A:*fx229,1
LOCALi%,e%,t$,T$
IFFNdebug:err$+=" ("+STR$ERL+")"
LOCALERROR
ONERRORLOCAL:PROCerror(ERR,REPORT$,ERL,TRUE)
!q%=err%:$(q%+4)=err$:SYS"Wimp_ReportError",q%,1,appl$
IFquit%ORFNdebug:PROCquit
ENDPROC

DEFPROCumess1
ref%=q%!8:task%=q%!4:IFtask%=taskhandle%:ENDPROC
CASE q%!16 OF
WHEN 0:PROCquit
WHEN &502:PROChelp
ENDCASE
ENDPROC

DEFPROCtemplate(T$,q%)
LOCAL c%:$workbuf%=T$
SYS "Wimp_LoadTemplate",,q%,curbuf%,endbuf%,-1,workbuf%TO,,curbuf%,,,,c%
IF c%=0 THEN ERROR 99,FNlookup1("E01",T$)
ENDPROC

DEFPROCset_templates
name$="<Flasher$Dir>.Templates"
SYS "OS_File",5,name$ TO found%,,,,bufsz%
bufsz%+=600
IF found%<>1 THEN ERROR 99,FNlookup("E02")
LOCALI%
DIM buffer% bufsz%:curbuf%=buffer%:endbuf%=buffer%+bufsz%
SYS"Wimp_OpenTemplate",,name$
PROCtemplate("ProgInfo",q%):SYS"Wimp_CreateWindow",,q%TOw_pinfo%
free%=endbuf%-curbuf%
SYS"Wimp_CloseTemplate"
ENDPROC

DEFPROCdefine_menus
LOCALt$
DIM mlist% 32
bufsz%=&200
DIM menufree% bufsz%:menuptr%=menufree%:menuend%=menufree%+bufsz%
menustart%=menufree%:RESTORE+2
t$=FNlookup1("Menu","w_pinfo%"):m_main%=FNcreate_menu(t$)
ENDPROC

DEFPROCclose_window(!q%)
SYS"Wimp_CloseWindow",,q%
ENDPROC

DEFFNindirect_addr(h%,i%)
PROCget_icon_state(h%,i%)
PROCchkii
=q%!28

DEFFNicon_length(h%,i%)
PROCget_icon_state(h%,i%)
PROCchkii
=q%!36

DEFPROCchkii
IF(q%!24AND&100)=0 ERROR 99,FNlookup("E03")
ENDPROC

DEFPROCget_icon_state(h%,i%)
!q%=h%:q%!4=i%
SYS"Wimp_GetIconState",,q%
ENDPROC

DEFPROCreadpointer
LOCALq%:q%=workbuf%
SYS"Wimp_GetPointerInfo",,q%
mx%=!q%:my%=q%!4:mb%=q%!8:mh%=q%!12:mi%=q%!16
ENDPROC

DEFPROCmouse(mx%,my%,mb%,mh%,mi%,mo%)
CASE mb% OF
WHEN 2:PROCmenu
WHEN 1,4:PROCselect
ENDCASE
ENDPROC

DEFPROCset_icontext(h%,i%,T$)
T$=LEFT$(T$,FNicon_length(h%,i%))
$FNindirect_addr(h%,i%)=T$
PROCiconupd(h%,i%)
ENDPROC

DEFPROCset_icon(h%,i%,bic%,eor%)
LOCALq%:q%=workbuf%
!q%=h%:q%!4=i%:q%!12=bic%:q%!8=eor%
SYS"Wimp_SetIconState",,q%
ENDPROC

DEFPROCiconupd(h%,i%):PROCset_icon(h%,i%,0,0):ENDPROC

DEFPROCselect
CASEmh%OF
WHEN-2:PROCshowcaret
ENDCASE
ENDPROC

DEFPROCmenu
CASEmh%OF
WHEN-2
my%=64+5*44:PROCpop(m_main%,-1)
ENDCASE
ENDPROC

DEFFNpar(t$,s$,RETURN i%)
LOCALi1%
i1%=i%+1:i%=INSTR(t$+s$,s$,i1%)
=MID$(t$,i1%,i%-i1%)

DEFFNcreate_menu(menu$)
IFmenufree%+28>menuend% ERROR99,FNlookup("E04")
LOCAL m%
i%=0
IF LEFT$(menu$,1)="#"i%=1:menutitle$=FNpar(menu$,",",i%)ELSEmenutitle$=""
menuptr%=menufree%+28:maxx%=LENmenutitle$
REPEATitem$=FNpar(menu$,",",i%)
PROCmenuitem(item$)
UNTIL item$=""
m%=menufree%
IF LENmenutitle$<=12 THEN
$m%=menutitle$
ELSE
m%!0=menuptr%:m%!4=-1:m%!8=LENmenutitle$+1:m%!28=m%!28 OR &100
$menuptr%=menutitle$:menuptr%+=m%!8
ENDIF
m%?12=7:m%?13=2:m%?14=7:m%?15=0
m%!16=(maxx%*8+6)*2:m%!20=44:m%!24=0
menufree%=menuptr%
=m%

DEFPROCmenuitem(text$)
IFtext$=""menuptr%!-24=(menuptr%!-24)OR&80:ENDPROC
IFmenuptr%+24>menuend%THEN ERROR99,FNlookup("E04")
LOCALi%,flg%
flg%=&00
i%=INSTR(text$,">")
IFi%>0subptr%=EVALMID$(text$,i%+1):text$=LEFT$(text$,i%-1)ELSEsubptr%=-1
i%=INSTR(text$,"}")
IFi%>0subptr%=EVALMID$(text$,i%+1):text$=LEFT$(text$,i%-1):flg%+=&08
IFRIGHT$(text$,1)="#"text$=LEFT$(text$):flg%+=&02
menuptr%!0=flg%:menuptr%!4=subptr%:menuptr%!8=&07000021
IFLEFT$(text$,1)="$"THEN
!menuptr%+=&04:menuptr%!8+=&100:i%=INSTR(text$,"(")
IFi%>0THENL%=VALMID$(text$,i%+1):text$=LEFT$(text$,i%-1)ELSEL%=12
menuptr%!12=EVALMID$(text$,2):menuptr%!16=-1:menuptr%!20=L%
text$=STRING$(L%," ")
ELSE
IFLENtext$<=12THEN
$(menuptr%+12)=text$
ELSE
I%=FNworkspace(LENtext$+1):$I%=text$
menuptr%!12=I%:menuptr%!16=-1:menuptr%!20=LENtext$+1
menuptr%!8=menuptr%!8 OR &100
ENDIF
IFLENtext$>maxx%maxx%=LENtext$
ENDIF
menuptr%+=24
ENDPROC

DEFFNworkspace(L%)
IF curbuf%+L%>endbuf%ERROR99,FNlookup("E05")
curbuf%+=L%:=curbuf%-L%

DEFPROCpop(menu%,tree%):LOCALI%
IFmenu%=m_main%THEN
PROCclearts(m_main%)
IFflashing%:PROCtick(m_main%,1)
IFautoscroll%:PROCtick(m_main%,2)
SYS"Wimp_GetCaretPosition",,q%:IF!q%<0PROCshade(m_main%,3)
ENDIF
SYS "Wimp_CreateMenu",,menu%,mx%-64,my%+32
openmenu%=menu%
ENDPROC

DEFPROCnomenu
SYS"Wimp_CreateMenu",,-1:openmenu%=-1
ENDPROC

DEFPROCmenuselect
LOCALI%
I%=0
REPEAT
mlist%!I%=q%!I%
I%+=4
UNTILq%!(I%-4)=-1
PROCreadpointer
CASEopenmenu%OF
WHENm_main%:PROCm_main
ENDCASE
IFmb%=1THEN
PROCpop(openmenu%,mlist%):ELSEPROCnomenu
ENDIF
ENDPROC

DEFPROCm_main
CASE!mlist%OF
WHEN1:PROCflash
WHEN2:autoscroll%=NOTautoscroll%
WHEN3:PROCshowcaret
WHEN4:PROCquit
ENDCASE
ENDPROC

DEFPROCclearts(mh%):LOCALI%:I%=mh%+28
IFmh%<menustart%ORmh%>menuend%:ENDPROC
REPEAT:!I%=!I%ANDNOT1:I%!8=I%!8ANDNOT&400000::I%+=24:UNTILI%!-24AND&80
ENDPROC

DEFPROCshade(mh%,I%):mh%!(28+8+24*I%)=mh%!(28+8+24*I%)OR&400000
DEFPROCtick(mh%,I%):mh%!(28+24*I%)=mh%!(28+24*I%)OR&01
ENDPROC

DEFPROCflash
IFflashing%THEN
PROCrestorec
flashing%=FALSE
ELSE
delay%=0
flashing%=TRUE
ENDIF
ENDPROC

DEFPROCbackgnd
IFfindcaret%PROCfindcaret
IFNOTflashing%:delay%=1E9:ENDPROC
SYS"Wimp_GetCaretPosition",,q%
SYS "OS_ReadMonotonicTime" TO delay%
IF!q%>=0THEN
q%!16=(q%!16)EOR(1<<25)
IFq%!16AND(1<<25):delay%+=offtime%:ELSEdelay%+=ontime%
PROCsetc
ELSE
delay%+=ontime%
ENDIF
ENDPROC

DEFPROCsetc
SYS"Wimp_SetCaretPosition",!q%,q%!4,q%!8,q%!12,q%!16,q%!20
ENDPROC

DEFPROCrestorec:IFNOTflashing%:ENDPROC
SYS"Wimp_GetCaretPosition",,q%
IF!q%>0:q%!16=(q%!16)ANDNOT(1<<25):PROCsetc
ENDPROC

DEFPROCshowcaret
LOCALh%,X%,Y%
SYS"Wimp_GetCaretPosition",,q%:IF!q%<0:VDU7:ENDPROC
X%=q%!8:Y%=q%!12:h%=!q%
SYS"Wimp_GetWindowState",h%,q%
IFautoscroll%THEN
IFX%<q%!20ORX%>q%!12-q%!4+q%!20q%!20=X%-(q%!12-q%!4)DIV2
IFY%>q%!24-32ORY%<q%!8-q%!16+q%!24q%!24=Y%+(q%!16-q%!8)DIV2
ENDIF
q%!28=-1:SYS"Wimp_SendMessage",2,q%,!q%
findcaret%=TRUE:delay%=0
ENDPROC

DEFPROCfindcaret:findcaret%=FALSE
LOCALX%,Y%,x%,y%,h%,I%,N%,T%,B%
SYS"Wimp_GetCaretPosition",,q%:IF!q%<0:VDU7:ENDPROC
X%=q%!8:Y%=q%!12
h%=!q%:SYS"Wimp_GetWindowState",h%,q%
IFX%<q%!20ORX%>q%!12-q%!4+q%!20ORY%>q%!24-32ORY%<q%!8-q%!16+q%!24:X%=q%!4:Y%=q%!8:ELSE:X%+=q%!4-q%!20:Y%+=q%!16-q%!24
MOUSE x%,y%,I%
N%=SQR((X%-x%)*(X%-x%)+(Y%-y%)*(Y%-y%))/32:IFN%=0N%=1
FORI%=1TON%
MOUSETOx%+(X%-x%)/N%*I%,y%+(Y%-y%)/N%*I%
MOUSET%,T%,B%:IF(B%AND1)I%=N%
WAIT:NEXT:MOUSETOX%,Y%:ENDPROC

DEFPROChelp
LOCALT$
CASE q%!32 OF
WHEN -2:IFflashing%T$=FNlookup("Off"):ELSET$=FNlookup("On")
T$=FNlookup1("H00",T$)
WHEN w_pinfo%:T$=FNlookup("H01")
ENDCASE
!q%=(25+LENT$)AND&FC:q%!16=&503:$(q%+20)=T$+CHR$0:q%!12=ref%
SYS"Wimp_SendMessage",17,q%,task%
ENDPROC

DEF PROCopen_messages(file$)
  LOCAL s%
  DIM __messbuf% 256
  SYS "MessageTrans_FileInfo",,file$ TO ,,s%
  DIM __filebuf% s%
  SYS "MessageTrans_OpenFile",__messbuf%,file$,__filebuf%
ENDPROC

DEF PROCclose_messages
  SYS "XMessageTrans_CloseFile",__messbuf%
ENDPROC

DEF FNstring(a%)
  LOCAL a$
  a$="":WHILE ?a% >= ASC" ":a$+=CHR$(?a%):a%+=1:ENDWHILE
=a$

DEF FNlookup1(token$,p1$)
  LOCAL t%
  SYS "MessageTrans_Lookup",__messbuf%,token$,__messbuf%+16,240,p1$ TO ,,t%
=FNstring(t%)

DEF FNlookup(token$)=FNlookup1(token$,"")
