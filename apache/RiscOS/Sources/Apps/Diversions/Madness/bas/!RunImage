REM > !Madness.!RunImage

REM Copyright 2016 Castle Technology Ltd
REM
REM Licensed under the Apache License, Version 2.0 (the "License");
REM you may not use this file except in compliance with the License.
REM You may obtain a copy of the License at
REM
REM     http://www.apache.org/licenses/LICENSE-2.0
REM
REM Unless required by applicable law or agreed to in writing, software
REM distributed under the License is distributed on an "AS IS" BASIS,
REM WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
REM See the License for the specific language governing permissions and
REM limitations under the License.
REM

DIMtaskid% 4:$taskid%="TASK"
message_handle%=FNopen_messagefile ("<Madness$Dir>.Messages")
SYS"Wimp_Initialise",200,!taskid%,FNmessage_lookup0 (message_handle%,"TaskID")TOversion%
`wbcol=&1:`tbcol=&2:`sco=&3:`mbcol=&B:`wfcol=&7:`tfcol=&7:`sci=&D:`tbcol2=&C
PROCvars
DIMq% 256,buffers%512,X%(255),Y%(255)
DIMmbuf% 256
FORX%=0TO255:X%(X%)=4*RND(3):Y%(X%)=4*RND(3):NEXT
Madness%=FNcwindow:madspeed%=20
DATA NotUsed,%1010011,`wfcol,`wbcol,0,-1024,1280,0,0,0,0,0,0
ONERRORPROCfinish:END

ONERRORPROCerrorbox:SYS CrMenu,,-1 : REM close menus!
q%!16=&400C1:PROCreceive(q%)
SYS Drag,,-1:PROCgetpointer:PROCpopup(Madness%)
SYS Time% TO timex%:timex%+=madspeed%:after%=Madness%
REPEAT
  SYS Poll%,0,q%,timex%TOA%
  CASE A% OF
  WHEN 0 :PROCnudge
  WHEN 3 :PROCcloseawindow(!q%)
  WHEN 17,18:PROCreceive(q%)
  ENDCASE
UNTIL FALSE

DEFPROCreceive(q%)
CASEq%!16 OF
WHEN0:PROCfinish:END
WHEN&400C1:!q%=4:q%!4=5:q%!8=11:q%!12=12:q%!16=-1
SYS49,q%,q%+20:ros=q%!28+1<<q%!20:tos=q%!32+1<<q%!24
ENDCASE
ENDPROC

DEFFNcwindow
  READA$,f%,fc%,bc%,x0%,y0%,x1%,y1%,cx0%,cy0%,cx1%,cy1%,nicons%
  A$=FNmessage_lookup0 (message_handle%,"Madness")
=FNcreate_window(cx0%,cy0%,cx1%,cy1%,0,0,f%,fc%,bc%,x0%,y0%,x1%,y1%,&2D,A$,nicons%)

DEFPROCcloseawindow(handle%)
PROCclose_window(handle%)
PROCfinish:END
ENDPROC

DEFPROCfinish
SYS"Wimp_CloseDown"
ENDPROC

DEFFNcreate_window(cx0%,cy0%,cx1%,cy1%,scx%,scy%,flags%,fc%,bc%,sx0%,sy0%,sx1%,sy1%,titlef%,title$,nicons%)
!q%=cx0%:q%!4=cy0%:q%!8=cx1%:q%!12=cy1%:q%!16=scx%:q%!20=scy%
q%!24=-2:q%!28=flags%
IFfc%=15ANDbc%=15THEN
  q%?32=15:q%?33=15:q%?34=fc%:q%?35=bc%
ELSEq%?32=`tfcol:q%?33=`tbcol:q%?34=fc%:q%?35=bc%
ENDIF
q%?36=`sco:q%?37=`tbcol:q%?38=`tbcol2:q%?39=0
q%!40=sx0%:q%!44=sy0%:q%!48=sx1%:q%!52=sy1%
q%!56=titlef%OR256
q%!60=&3000      : REM work area flags
q%!64=0:q%!68=0  : REM reserved
DIMtb%LEN(title$+CHR$(0))
$tb%=title$+CHR$(0)
q%!72=tb%
q%!76=-1
q%!80=LEN(title$)+1
q%!84=nicons%
LOCALI%,x0%,y0%,x1%,y1%,f%,data$:I%=q%+88
WHILEnicons%>0
  READx0%,y0%,x1%,y1%,f%,fc%,bc%,data$
  I%!0=x0%:I%!4=y0%:I%!8=x1%:I%!12=y1%
  IF(f%AND&40)=0THENbc%=fc%+(bc%<<4)
  I%!16=f%+(bc%<<24)
  IF(f%AND&100)THEN
    READL%:B%=FNbuffer(L%):I%!20=B%:$B%=data$:I%!24=-1:I%!28=L%
  ELSE
    $(I%+20)=data$
  ENDIF
  I%=I%+32:nicons%=nicons%-1
ENDWHILE
SYS CreateW,,q%TOhandle%
=handle%

DEFPROCfront(handle%)
IFhandle%=-1THENENDPROC
PROCgetw(handle%)
PROCopen_window(handle%,x0%,y0%,x1%,y1%,scx%,scy%,-1)
ENDPROC

DEFPROCpopup(handle%)
PROCgetw(handle%)
xo%=0:yo%=100
PROCopen_window(handle%,xo%,yo%,xo%,yo%,scx%,scy%,-2)
ENDPROC

DEFPROCopen_window(handle%,x0%,y0%,x1%,y1%,scx%,scy%,bhandle%)
IFhandle%=-1THENENDPROC
q%!0=handle%:q%!28=bhandle%
q%!4=x0%:q%!8=y0%:q%!12=x1%:q%!16=y1%
q%!20=scx%:q%!24=scy%
SYS OpenW,,q%
ENDPROC

DEFPROCgetw(handle%)
!q%=handle%:SYS GetW,,q%
PROCinfo(q%+4):bhandle%=q%!28:flags%=q%!32
ENDPROC

DEFPROCinfo(q%)
x0%=!q%:y0%=q%!4:x1%=q%!8:y1%=q%!12:scx%=q%!16:scy%=q%!20
bx%=x0%-scx%:by%=y1%-scy%
ENDPROC

DEFPROCnudge
LOCALq%:PROCtick:q%=buffers%
LOCALERROR
ONERRORLOCALafter%=Madness%:RESTOREERROR:PROCpopup(Madness%):ENDPROC
!q%=after%:SYS gws%,,q%:after%=q%!28
IFafter%=-1ERROR
!q%=after%:SYS gws%,,q%
a%=after%AND255
IFq%!12>ros-64 X%(a%)=-RND(3)*4ELSEIFq%!4<10 X%(a%)=RND(3)*4
q%!4+=X%(a%):q%!12+=X%(a%)
IFq%!16>tos-64 Y%(a%)=-RND(3)*4ELSEIFq%!8<100 Y%(a%)=RND(3)*4
q%!8+=Y%(a%):q%!16+=Y%(a%)
SYS"Wimp_SendMessage",2,q%,after%
ENDPROC

DEFPROCtick
SYS Time%TOtimeslip%
WHILEtimeslip%>=timex%:timex%+=madspeed%:ENDWHILE
ENDPROC

DEFPROCclose_window(handle%)
!q%=handle%:SYS CloseW,,q%
ENDPROC

DEFPROCvars
Wimp    =(1<<18)+(3<<6)
CreateW =Wimp+1
OpenW   =Wimp+5
CloseW  =Wimp+6
RedrawW =Wimp+8
UpdateW =Wimp+9
GetR%   =Wimp+10
GetW    =Wimp+11
GetP    =Wimp+15
Drag    =Wimp+16
CrMenu  =Wimp+20
DcMenu  =Wimp+21
SYS"OS_SWINumberFromString",,"Wimp_PollIdle" TO Poll%
SYS"OS_SWINumberFromString",,"OS_ReadMonotonicTime" TO Time%
SYS"OS_SWINumberFromString",,"Wimp_GetWindowState" TO gws%
ENDPROC

DEFPROCgetpointer
SYS GetP,,q%
mousex%=!q%:mousey%=q%!4:b%=q%!8:handle%=q%!12:icon%=q%!16:ob%=q%!20
ENDPROC

DEFPROCerrorbox
ONERRORPROCfinish:END
SYS Drag,,-1
!buffers%=ERR:$(buffers%+4)=FNmessage_lookup2 (message_handle%,"IntErr",REPORT$,STR$ERL)+CHR$0
SYS"Wimp_ReportError",buffers%,1,FNmessage_lookup0 (message_handle%,"Madness")
PROCfinish:END
ENDPROC

DEFFNopen_messagefile(name$)
LOCALmh%
SYS"OS_Module",6,,,16TO,,mh%
SYS"MessageTrans_OpenFile",mh%,name$,0
=mh%

DEFFNmessage_lookup0(mh%,tag$)
LOCALs$
SYS"MessageTrans_Lookup",mh%,tag$,0,0,0,0,0,0TO,,s$
=s$

DEFFNmessage_lookup2(mh%,tag$,arg1$,arg2$)
LOCALs$
SYS"MessageTrans_Lookup",mh%,tag$,mbuf%,256,arg1$,arg2$,0,0TO,,s$
=s$
