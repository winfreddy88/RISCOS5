REM >!Patience.!RunImage

REM Copyright 2014 Castle Technology Ltd
REM
REM Licensed under the Apache License, Version 2.0 (the "License");
REM you may not use this file except in compliance with the License.
REM You may obtain a copy of the License at
REM
REM     http://www.apache.org/licenses/LICENSE-2.0
REM
REM Unless required by applicable law or agreed to in writing, software
REM distributed under the License is distributed on an "AS IS" BASIS,
REM WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
REM See the License for the specific language governing permissions and
REM limitations under the License.
REM

REM Authors: Acorn, CWraight


REM-----------------------------------------------------------------------------
REM // Initialise application and load resources //
REM-----------------------------------------------------------------------------

PROCmess_load("<Patience$Dir>.Messages")
PROCwimp_init
PROCgame_init

REM-----------------------------------------------------------------------------
REM // Poll loop //
REM-----------------------------------------------------------------------------

WHILE NOT AppQuit%
 PROCwimp_poll
ENDWHILE
SYS "Wimp_CloseDown"
PROCmess_close
END

REM-----------------------------------------------------------------------------
REM // Game //
REM-----------------------------------------------------------------------------

DEF PROCgame_init
LOCAL file%,flags%,command$,value$
REM Global variables
DIM CardSpace% 52                       :REM Array for use with pack
Deck$=STRING$(52," ")                   :REM Deck array
DeckPlace%=0                            :REM Location in Deck
DealNumber%=0                           :REM Number of cards dealt at once
DIM Table$(7)                           :REM Table columns
InPlay%=FALSE                           :REM Game is in play
Played%=0                               :REM Games played
Won%=0                                  :REM Games won
Source$=""                              :REM Source area of dragged card
Index%=0                                :REM Position of dragged card in column
DIM Suit%(3)                            :REM Suit Array
DIM Suit$(3)                            :REM Suit sprite names
Suit$(0)="club":Suit$(1)="diamond"      :REM Suit sprite names
Suit$(2)="heart":Suit$(3)="spade"       :REM Suit sprite names
Wx%=0                                   :REM Game window x origin
Wy%=0                                   :REM Game window y origin
TX%=8                                   :REM Table left edge
TY%=-66                                 :REM Table top edge
CW%=68                                  :REM Card width
CH%=88                                  :REM Card height
HH%=40                                  :REM Half card height
DX%=CW%*10                              :REM Deck coords
DY%=TY%-CH%*3                           :REM Deck coords
DP%=CW%*9                               :REM Pile coords
SX%=CW%*8                               :REM Stack coords
WorkX%=12*CW%+TX%*2                     :REM Work area width
WorkY%=18*HH%+CH%-TY%                   :REM Work area height
Option_WinX%=200                        :REM Option defaults
Option_WinY%=300                        :REM Option defaults
Option_ExtentX%=WorkX%                  :REM Option defaults
Option_ExtentY%=WorkY%                  :REM Option defaults
Option_OnlyKings%=TRUE                  :REM Option defaults
Option_RevCards%=FALSE                  :REM Options defaults
Option_NumberOver%=3                    :REM Options defaults
Option_Sound%=FALSE                     :REM Options defaults
REM Read user choices from script in Choices: (if present)
SYS "XOS_Find",%01001111,"Choices:Patience.Choices" TO file%;flags%
IF((flags% AND 1)=0) AND(file%<>0) THEN
 WHILE NOT EOF#file%
  command$=GET$#file%
  IF INSTR(command$,":") THEN
   value$=RIGHT$(command$,LEN(command$)-INSTR(command$,":"))
   command$=LEFT$(command$,INSTR(command$,":")-1)
   CASE command$ OF
    WHEN "RevCards":
     IF value$="Y" THEN Option_RevCards%=TRUE ELSE Option_RevCards%=FALSE
    WHEN "OnlyKings":
     IF value$="Y" THEN Option_OnlyKings%=TRUE ELSE Option_OnlyKings%=FALSE
    WHEN "NumberOver":
     Option_NumberOver%=VAL(value$)
    WHEN "Sound":
     IF value$="Y" THEN Option_Sound%=TRUE ELSE Option_Sound%=FALSE
   ENDCASE
  ENDIF
 ENDWHILE
 CLOSE#file%
ENDIF
REM Read window position, if stored
SYS "XOS_Find",%01001111,"Choices:Patience.WinPos" TO file%;flags%
IF((flags% AND 1)=0) AND (file%<>0) THEN
 WHILE NOT EOF#file%
  command$=GET$#file%
  IF INSTR(command$,":") THEN
   value$=RIGHT$(command$,LEN(command$)-INSTR(command$,":"))
   command$=LEFT$(command$,INSTR(command$,":")-1)
   CASE command$ OF
    WHEN "WinX":
     Option_WinX%=VAL(value$)
    WHEN "WinY":
     Option_WinY%=VAL(value$)
    WHEN "ExtentX":
     Option_ExtentX%=VAL(value$)
    WHEN "ExtentY":
     Option_ExtentY%=VAL(value$)
   ENDCASE
  ENDIF
 ENDWHILE
 CLOSE#file%
ENDIF
PROCgame_shuffle
ENDPROC

DEF PROCgame_shuffle
REM Shuffle the pack
LOCAL i%,j%,k%,n%
@%=5
n%=RND(TIME OR &80000000)
Deck$="":k%=0
FOR i%=0 TO 3
 FOR j%=1 TO 13
  CardSpace%?k%=i%+j%*4
  k%=k%+1
 NEXT
NEXT
FOR i%=1 TO 52
 REPEAT:j%=RND(52)-1:UNTIL CardSpace%?j%
 Deck$+=CHR$(CardSpace%?j%+128)
 CardSpace%?j%=0
NEXT
FOR i%=1 TO 7
 Table$(i%)=LEFT$(Deck$,7-i%)
 Table$(i%)+=CHR$(ASCMID$(Deck$,8-i%) AND 127)
 Deck$=MID$(Deck$,9-i%)
NEXT
DeckPlace%=0:Table$=Deck$:Deck$=""
FOR i%=1 TO LEN(Table$)
 Deck$+=CHR$(ASCMID$(Table$,i%) AND 127)
NEXT
FOR i%=0 TO 3:Suit%(i%)=i%:NEXT
InPlay%=TRUE
DealNumber%=Option_NumberOver%
ENDPROC

DEF PROCgame_drawboard
REM Draw the main game board
LOCAL col%,more%,rx0%,rx1%,ry0%,ry1%,cmin%,cmax%,i%,j%
SYS "Wimp_RedrawWindow",,Block% TO more%
Wx%=Block%!4-Block%!20
Wy%=Block%!16-Block%!24
WHILE more%
 rx0%=Block%!28:rx1%=Block%!36
 ry0%=Block%!32:ry1%=Block%!40
 REM Draw background
 PROCwimp_pixtrans(SpriteArea%,"tile")
 FOR i%=rx0% TO rx1% STEP CW%
  FOR j%=ry0% TO ry1% STEP CH%
   PROCwimp_plotsprite(SpriteArea%,"tile",i%,j%,FALSE)
  NEXT
 NEXT
 REM Draw Table
 cmin%=(rx0%-(Wx%+TX%)) DIV CW%
 cmax%=(rx1%-(Wx%+TX%)) DIV CW%:IF cmax%>6 THEN cmax%=6
 IF cmin%<7 THEN
  FOR col%=cmin% TO cmax%
   PROCgame_drawcolumn(Table$(col%+1),Wx%+TX%+col%*CW%,Wy%+TY%,1)
  NEXT
 ENDIF
 REM Draw Stack
 cmin%=(rx0%-Wx%) DIV CW%-8:IF cmin%<0 THEN cmin%=0
 cmax%=(rx1%-Wx%) DIV CW%-8:IF cmax%>3 THEN cmax%=3
 IF cmax%>-1 THEN
  IF cmin%<4 THEN
   FOR col%=cmin% TO cmax%
    PROCgame_drawcard(0,Suit%(col%),Wx%+SX%+(col%*CW%),Wy%+TY%-CH%)
   NEXT
  ENDIF
 ENDIF
 REM Draw Deck
 PROCgame_drawdeck
 SYS "Wimp_GetRectangle",,Block% TO more%
ENDWHILE
ENDPROC

DEF PROCgame_drawdeck
REM Draw the Deck
IF LEN(Deck$) THEN
 IF DeckPlace%=LEN(Deck$) THEN
  PROCgame_drawnodeck(Wx%+DX%,Wy%+DY%)
 ELSE
  PROCgame_drawback(Wx%+DX%,Wy%+DY%)
 ENDIF
 IF DeckPlace%>0 THEN
  PROCgame_drawcard(0,ASCMID$(Deck$,DeckPlace%,1),Wx%+DP%,Wy%+DY%)
 ELSE
  PROCgame_drawnopile(Wx%+DP%,Wy%+DY%)
 ENDIF
ELSE
 PROCgame_drawnopile(Wx%+DP%,Wy%+DY%)
 PROCgame_drawnodeck(Wx%+DX%,Wy%+DY%)
ENDIF
ENDPROC

DEF PROCgame_drawcolumn (col$,x%,y%,start%)
REM Draw a column of cards
LOCAL i%,j%
IF LEN(col$)=0 OR start%>LEN(col$) THEN ENDPROC
IF LEN(col$)>1 THEN
 PROCwimp_pixtrans(SpriteArea%,"half")
 FOR i%=1 TO LEN(col$)-1
  IF i%>=start% THEN
   j%=ASCMID$(col$,i%)
   IF j%>127 THEN
    PROCwimp_plotsprite(SpriteArea%,"half",x%,y%-44,TRUE)
   ELSE
    PROCgame_drawcard(1,j%,x%,y%-44)
   ENDIF
  ENDIF
  y%-=HH%
 NEXT
ENDIF
y%-=CH%
j%=ASCMID$(col$,LEN(col$))
IF j%>127 THEN
 PROCgame_drawback(x%,y%)
ELSE
 PROCgame_drawcard(0,j%,x%,y%)
ENDIF
ENDPROC

DEF PROCgame_drawcard (type%,card%,x%,y%)
REM Draw a card
LOCAL sprite$
CASE type% OF
 WHEN 0:sprite$=Suit$(card% AND 3)
  IF (card%>>2)=0 THEN sprite$="s"+sprite$
 WHEN 1:sprite$="half"+Suit$(card% AND 3)
ENDCASE
PROCwimp_pixtrans(SpriteArea%,sprite$)
PROCwimp_plotsprite(SpriteArea%,sprite$,x%,y%,TRUE)
IF (card% EOR card%>>1) AND 1 THEN
 SYS SWI_FONTCOLOURS%,0,&FFFFFF00,&0000AA00,14
ELSE
 SYS SWI_FONTCOLOURS%,0,&FFFFFF00,&00000000,14
ENDIF
card%=card%>>2
IF type%=1 THEN y%-=44
SYS SWI_SETFONT%,Font%
IF (card%>1 AND card%<10) THEN SYS SWI_FONTPAINT%,Font%,STR$card%,16,x%+34,y%+54
IF card%=1 THEN SYS SWI_FONTPAINT%,Font%,"A",16,x%+32,y%+54
IF card%=10 THEN SYS SWI_FONTPAINT%,Font%,"10",16,x%+26,y%+54
IF card%=11 THEN SYS SWI_FONTPAINT%,Font%,"J",16,x%+34,y%+54
IF card%=12 THEN SYS SWI_FONTPAINT%,Font%,"Q",16,x%+30,y%+54
IF card%=13 THEN SYS SWI_FONTPAINT%,Font%,"K",16,x%+32,y%+54
ENDPROC

DEF PROCgame_drawdragcard (type%,card%,x%,y%,opaque%)
LOCAL i%,r0%,r1%,r2%,r3%,sprite$
REM Switch output to sprite and plot the card part
SYS SWI_SPRITE%,&13C,DragArea%,"drag",0 TO r0%,r1%,r2%,r3%
PROCgame_drawcard(type%,card%,x%,y%)
SYS SWI_SPRITE%,r0%,r1%,r2%,r3%
REM Choose the template from the suit
CASE type% OF
 WHEN 0:sprite$=Suit$(card% AND 3)
  IF (card%>>2)=0 THEN sprite$="s"+sprite$
 WHEN 1:sprite$="half"+Suit$(card% AND 3)
ENDCASE
REM Map all input colours to the mask opaque colour
FOR i%=0 TO 255
 PixTrans%?i%=opaque%
NEXT
REM Switch output to mask and plot the opaque pixels
SYS SWI_SPRITE%,&13D,DragArea%,"drag",0 TO r0%,r1%,r2%,r3%
PROCwimp_plotsprite(SpriteArea%,sprite$,x%,y%,TRUE)
SYS SWI_SPRITE%,r0%,r1%,r2%,r3%
ENDPROC

DEF PROCgame_drawback (x%,y%)
REM Draw a card back
PROCwimp_pixtrans(SpriteArea%,"back")
PROCwimp_plotsprite(SpriteArea%,"back",x%,y%,TRUE)
ENDPROC

DEF PROCgame_drawnodeck (x%,y%)
REM Draw the empty space where the Deck was
PROCwimp_pixtrans(SpriteArea%,"empty")
PROCwimp_plotsprite(SpriteArea%,"empty",x%,y%,FALSE)
ENDPROC

DEF PROCgame_drawnopile (x%,y%)
REM Draw the empty space where the Pile was
PROCwimp_pixtrans(SpriteArea%,"tile")
PROCwimp_plotsprite(SpriteArea%,"tile",x%,y%,FALSE)
ENDPROC

DEF PROCgame_nextcard
REM Turn the next card over from the Deck
LOCAL next$,more%,i%
IF LEN(Deck$)=0 THEN
 IF Option_Sound% THEN VDU 7
 ENDPROC
ENDIF
IF DeckPlace%=LEN(Deck$) THEN DeckPlace%=0
IF Option_RevCards% THEN
 next$=""
 FOR i%=DealNumber% TO 1 STEP-1
  next$+=MID$(Deck$,DeckPlace%+i%,1)
 NEXT
ELSE
 next$=MID$(Deck$,DeckPlace%+1,DealNumber%)
ENDIF
Deck$=LEFT$(Deck$,DeckPlace%)+next$+MID$(Deck$,DeckPlace%+DealNumber%+1)
DeckPlace%+=LEN(next$)
MID$(Deck$,DeckPlace%)=CHR$(ASCMID$(Deck$,DeckPlace%) AND 127)
more%=FNgamewin_update(DP%,DY%,DX%+CW%,DY%+CH%)
WHILE more%
 PROCgame_drawdeck
 SYS "Wimp_GetRectangle",,Block% TO more%
ENDWHILE
ENDPROC

DEF PROCgame_dragcard (source$,mx%,my%)
REM Start a card drag
LOCAL col%,height%,i%,x0%,y0%,x1%,y1%,mx%,my%,newmem%,oldmem%,val%,ncolour%
Block%!0=GameWin%
SYS "Wimp_GetWindowState",,Block%
x0%=Block%!4:y0%=Block%!8
x1%=Block%!12:y1%=Block%!16
SYS "Wimp_GetPointerInfo",,Block%
mx%=Block%!0:my%=Block%!4
IF FNgame_dragcheck(source$)=FALSE THEN
 SYS "OS_Byte",161,28 TO ,,val%
 SolidDrag%=(val% AND (1<<1))<>0
 IF SolidDrag% THEN
  REM Estimate space for biggest possible DragASprite sprite
  SYS "OS_ReadModeVariable",-1,3 TO ,,ncolour%
  CASE ncolour% OF
   WHEN 1,3,15,63,255:
    dragsize%=(((((12*HH%)+CH%)*CW%)*16)/8)+2048:REM 8bpp+8bpp mask, with palette
   OTHERWISE
    dragsize%=(((((12*HH%)+CH%)*CW%)*33)/8):REM 32bpp+1bpp mask, no palette
  ENDCASE
  dragsize%=dragsize%+16+44:REM Sprite area and sprite header
  dragsize%=(dragsize%+4095) AND NOT 4095:REM Page round up
  SYS"Wimp_SlotSize",-1,-1 TO oldmem%
  SYS"Wimp_SlotSize",oldmem%+dragsize%,-1 TO newmem%
  IF (newmem%-oldmem%) < dragsize% THEN
   REM Not enough memory to solid drag
   SYS"Wimp_SlotSize",oldmem%,-1
   SolidDrag%=FALSE
  ELSE
   REM Build the sprite up there
   DragArea%=HIMEM
  ENDIF
 ENDIF
 IF SolidDrag% THEN
  !DragArea%=dragsize%:DragArea%!8=16
  SYS SWI_SPRITE%,&109,DragArea%
  CASE source$ OF
   WHEN "P":
    PROCgame_makesprite(0,CW%,CH%)
    Block%!0=mx%-CW% DIV 2
    Block%!4=my%+CH% DIV 2
    Block%!8=mx%+CW% DIV 2
    Block%!12=my%-CH% DIV 2
   OTHERWISE:
    col%=(ASCsource$-64)-1
    height%=CH%
    IF Index%<LEN(Table$(col%+1)) THEN
     FOR i%=Index% TO LEN(Table$(col%+1))-1
      height%+=HH%
     NEXT
    ENDIF
    PROCgame_makesprite(col%+1,CW%,height%)
    Block%!0=mx%-CW% DIV 2
    Block%!4=my%+CH% DIV 2
    Block%!8=mx%+CW% DIV 2
    Block%!12=my%-(height%-CH% DIV 2)
  ENDCASE
  SYS "DragASprite_Start",80,DragArea%,"drag",Block%
  SYS SWI_SPRITE%,&119,DragArea%,"drag"
  REM Return memory as DragASprite takes a copy of sprite
  SYS"Wimp_SlotSize",oldmem%,-1
 ELSE
  Block%!0=0
  Block%!4=5
  CASE source$ OF
   WHEN "P":
    Block%!8=mx%-CW% DIV 2
    Block%!12=my%+CH% DIV 2
    Block%!16=mx%+CW% DIV 2
    Block%!20=my%-CH% DIV 2
   OTHERWISE:
    col%=(ASCsource$-64)-1
    height%=CH%
    IF Index%<LEN(Table$(col%+1)) THEN
     FOR i%=Index% TO LEN(Table$(col%+1))-1
      height%+=HH%
     NEXT
    ENDIF
    Block%!8=mx%-CW% DIV 2
    Block%!12=my%+CH% DIV 2
    Block%!16=mx%+CW% DIV 2
    Block%!20=my%-(height%-CH% DIV 2)
  ENDCASE
  Block%!24=x0%
  Block%!28=y0%
  Block%!32=x1%
  Block%!36=y1%
  SYS "Wimp_DragBox",,Block%
 ENDIF
ENDIF
ENDPROC

DEF PROC game_makesprite (c%,width%,height%)
REM Builds a sprite to be dragged
LOCAL xeig%,yeig%,mode%,r0%,r1%,r2%,r3%,ncolour%,opaque%,paletted%
LOCAL i%,j%
REM Get Mode info
SYS "OS_ReadModeVariable",-1,3 TO ,,ncolour%
SYS "OS_ReadModeVariable",-1,4 TO ,,xeig%
SYS "OS_ReadModeVariable",-1,5 TO ,,yeig%
CASE ncolour% OF
 WHEN 1,3,15,63,255:
  REM Generate 8bpp in all these cases, prefer old mode numbers
  opaque%=255:bpp%=8:paletted%=TRUE
  CASE yeig%+(xeig%<<4) OF
   WHEN &12: mode%=15
   WHEN &22: mode%=13
   WHEN &11: mode%=28
   OTHERWISE:mode%=(4<<27)+((180>>yeig%)<<14)+((180>>xeig%)<<1)+(1<<0):opaque%=1
  ENDCASE
 OTHERWISE:
  REM New sprite type with lots of colours
  mode%=(6<<27)+((180>>yeig%)<<14)+((180>>xeig%)<<1)+(1<<0):opaque%=1
ENDCASE
REM Create a sprite, give it a mask
SYS SWI_SPRITE%,&10F,DragArea%,"drag",0,width% >> xeig%,height% >> yeig%,mode%
SYS SWI_SPRITE%,&11D,DragArea%,"drag"
IF paletted% THEN SYS SWI_SPRITE%,&125,DragArea%,"drag",1
REM Start with an entirely transparent mask
SYS SWI_SPRITE%,&13D,DragArea%,"drag",0 TO r0%,r1%,r2%,r3%
SYS "OS_SetColour",1<<4,0:CLG
SYS SWI_SPRITE%,r0%,r1%,r2%,r3%
REM Plot card sprites
CASE c% OF
 WHEN 0:
  PROCgame_drawdragcard(0,ASCMID$(Deck$,DeckPlace%,1),0,0,opaque%)
 OTHERWISE:
  j%=height%
  IF Index%<LEN(Table$(c%)) THEN
   FOR i%=Index% TO LEN(Table$(c%))-1
    PROCgame_drawdragcard(1,ASCMID$(Table$(c%),i%),0,j%-44,opaque%)
    j%-=HH%
   NEXT
  ENDIF
  j%-=CH%
  PROCgame_drawdragcard(0,ASCMID$(Table$(c%),LEN(Table$(c%))),0,j%,opaque%)
ENDCASE
ENDPROC

DEF FNgame_dragcheck (type$)
REM Check to see if a drag is legal
Source$=type$
IF type$="P" THEN
 IF DeckPlace%=0 OR LEN(Deck$)=0 THEN
  IF Option_Sound% THEN VDU 7
  =TRUE
 ENDIF
ENDIF
IF type$<>"P" THEN
 IF LEN(Table$(ASCtype$-64))=0 THEN
  IF Option_Sound% THEN VDU 7
  =TRUE
 ENDIF
ENDIF
=FALSE

DEF PROCgame_dropcard (x%,y%)
REM Handle completed drags
x%=x%-Wx%:y%=y%-Wy%
IF x%>SX% AND x%<SX%+(CW%*4) AND y%>TY%-CH% AND y%<TY% THEN
 PROCgame_move("S")
ENDIF
IF x%>TX% AND x%<TX%+CW%*7 AND y%<TY% THEN
 PROCgame_move(CHR$((x%-TX%) DIV CW%+ASC"A"))
ENDIF
ENDPROC

DEF PROCgame_move (area$)
REM Move a card from one area to another
LOCAL card%,suit%,scol%,tcol%,tsuit%,len%,i%
IF Source$<>"P" THEN scol%=ASCSource$-64
tcol%=ASCarea$
IF tcol%=ASC"S" THEN
 REM Card has been dragged to the Stack
 IF Source$="P" THEN
  card%=ASCMID$(Deck$,DeckPlace%)
 ELSE
  card%=ASCMID$(Table$(scol%),LEN(Table$(scol%)))
 ENDIF
 suit%=card% AND 3
 IF card%<>Suit%(suit%)+4 THEN
  IF Option_Sound% THEN VDU 7
  ENDPROC
 ENDIF
 Suit%(suit%)=card%
 IF Source$="P" THEN
  PROCgame_removepile
 ELSE
  PROCgame_removetable(scol%,1)
 ENDIF
 PROCgame_addstack(suit%)
ELSE
 REM Card has been dragged to the Table
 IF tcol%<ASC"A" OR tcol%>ASC"G" THEN
  IF Option_Sound% THEN VDU 7
  ENDPROC
 ENDIF
 tsuit%=ASCRIGHT$(Table$(tcol%-64),1)
 len%=LEN(Table$(tcol%-64))
 IF Source$="P" THEN
  card%=ASCMID$(Deck$,DeckPlace%)
  IF FNgame_movecheck(card%,tsuit%)=0 THEN
   IF Option_Sound% THEN VDU 7
   ENDPROC
  ENDIF
  Table$(tcol%-64)+=CHR$card%
  PROCgame_removepile
 ELSE
  i%=0
  REPEAT
   i%+=1:card%=ASCMID$(Table$(scol%),i%)
  UNTIL FNgame_movecheck(card%,tsuit%) OR i%>LEN(Table$(scol%))
  IF i%>LEN(Table$(scol%)) THEN
   IF Option_Sound% THEN VDU 7
   ENDPROC
  ENDIF
  Table$(tcol%-64)+=MID$(Table$(scol%),i%)
  PROCgame_removetable(scol%,LEN(Table$(scol%))-i%+1)
 ENDIF
 PROCgame_addtable(tcol%,len%)
ENDIF
ENDPROC

DEF PROCgame_removepile
REM Remove a card from the Pile
LOCAL more%
DeckPlace%-=1
Deck$=LEFT$(Deck$,DeckPlace%)+MID$(Deck$,DeckPlace%+2)
PROCwimp_seticontext(ToolWin%,1,STR$LEN(Deck$)+FNmess_get("Left"))
more%=FNgamewin_update(DP%,DY%,DP%+CW%,DY%+CH%)
WHILE more%
 PROCgame_drawdeck
 SYS "Wimp_GetRectangle",,Block% TO more%
ENDWHILE
ENDPROC

DEF PROCgame_removetable (c%,cards%)
REM Move card(s) from a column on the Table
LOCAL oldlen%,newlen%,bottom%,top%,cx%,more%
oldlen%=LEN(Table$(c%))
newlen%=oldlen%-cards%
cx%=TX%+(c%-1)*CW%
Table$(c%)=LEFT$(Table$(c%),newlen%)
IF LEN(Table$(c%)) THEN Table$(c%)=LEFT$(Table$(c%),LEN(Table$(c%))-1)+CHR$(ASCRIGHT$(Table$(c%),1) AND 127)
more%=FNgamewin_update(cx%,TY%-(oldlen%*HH%)-CH%,cx%+CW%,TY%)
WHILE more%
 bottom%=Block%!32
 top%=Block%!32+HH%*(cards%+2)
 PROCwimp_pixtrans(SpriteArea%,"tile")
 REPEAT
  PROCwimp_plotsprite(SpriteArea%,"tile",Wx%+cx%,bottom%,FALSE)
  bottom%+=CH%
 UNTIL bottom%>top%
 IF newlen%>0 THEN PROCgame_drawcolumn(Table$(c%),Wx%+cx%,Wy%+TY%,newlen%-1)
 SYS "Wimp_GetRectangle",,Block% TO more%
 ENDWHILE
ENDPROC

DEF PROCgame_addstack (suit%)
REM Add a card to the Stack
LOCAL more%
more%=FNgamewin_update(SX%+(suit%*CW%),TY%-CH%,SX%+(suit%*CW%)+CW%,TY%)
WHILE more%
 PROCgame_drawcard(0,Suit%(suit%),Wx%+SX%+(suit%*CW%),Wy%+TY%-CH%)
 SYS "Wimp_GetRectangle",,Block% TO more%
ENDWHILE
ENDPROC

DEF PROCgame_addtable (tcol%,len%)
REM Add card(s) to a Table column
LOCAL more%
more%=FNgamewin_update(TX%+(tcol%-ASC"A")*CW%,TY%-(LEN(Table$(tcol%-64))*HH%)-CH%,TX%+(tcol%-64)*CW%,-len%*HH%)
WHILE more%
 PROCgame_drawcolumn(Table$(tcol%-64),Wx%+TX%+(tcol%-65)*CW%,Wy%+TY%,len%)
 SYS "Wimp_GetRectangle",,Block% TO more%
ENDWHILE
ENDPROC

DEF FNgame_movecheck (card%,tsuit%)
REM Check to see if a card can be moved
LOCAL s%
IF tsuit%=-1 THEN
 IF Option_OnlyKings% THEN =card% DIV 4=13 ELSE =card%<127
ENDIF
s%=(tsuit% EOR card%) AND 3
IF s%=0 OR s%=3 THEN =FALSE
=tsuit% DIV 4=card% DIV 4+1

DEF PROCgame_resign
REM Resign a game
LOCAL i%,j%
FOR i%=1 TO 7
 FOR j%=1 TO LEN(Table$(i%))
  MID$(Table$(i%),j%,1)=CHR$(ASCMID$(Table$(i%),j%,1) AND 127)
 NEXT
NEXT
InPlay%=FALSE:DealNumber%=1
SYS "Wimp_ForceRedraw",GameWin%,0,-WorkY%,WorkX%,0
ENDPROC

DEF PROCgame_newgame
REM Start a new game
LOCAL won%,i%,j%
won%=TRUE
FOR i%=1 TO 7
 IF LEN(Table$(i%)) THEN
  FOR j%=1 TO LEN(Table$(i%))
   IF ASCMID$(Table$(i%),j%)>127 THEN won%=FALSE
  NEXT
 ENDIF
NEXT
Played%+=1
IF Deck$="" AND won%=TRUE THEN Won%+=1
PROCgame_shuffle
PROCwimp_seticontext(ToolWin%,0,FNmess_get("Won")+STR$Won%+"/"+STR$Played%)
PROCwimp_seticontext(ToolWin%,1,STR$LEN(Deck$)+FNmess_get("Left"))
SYS "Wimp_ForceRedraw",GameWin%,0,-WorkY%,WorkX%,0
ENDPROC

REM-----------------------------------------------------------------------------
REM // GameWin //
REM-----------------------------------------------------------------------------

DEF PROCgamewin_init
REM Resize and initialise the Game window
Block%!0=0
Block%!4=-WorkY%
Block%!8=WorkX%
Block%!12=0
SYS "Wimp_SetExtent",GameWin%,Block%
SYS "Wimp_ResizeIcon",ToolWin%,1,WorkX%-252,-52,WorkX%,0
PROCwimp_seticontext(ToolWin%,0,FNmess_get("Won")+STR$Won%+"/"+STR$Played%)
PROCwimp_seticontext(ToolWin%,1,STR$LEN(Deck$)+FNmess_get("Left"))
ENDPROC

DEF PROCgamewin_click (x%,y%,button%)
REM Handle clicks in the game window
LOCAL mx%,my%,c$,col%,min%,hlen%
SYS "Wimp_SetCaretPosition",GameWin%,-1,0,0,1<<25
Block%!0=GameWin%
SYS "Wimp_GetWindowState",,Block%
mx%=x%-(Block%!4-Block%!20)
my%=y%-(Block%!16-Block%!24)
CASE button% OF
 WHEN MENU%:
  SYS "Wimp_GetPointerInfo",,Block%
  SYS "Wimp_CreateMenu",,MainMenu%,Block%!0-64,Block%!4
  MenuOpen%=MainMenu%
 WHEN SELECT%:
  IF mx%>=DX% AND mx%<=DX%+CW%-6 AND my%>DY% AND my%<DY%+CH% THEN
   PROCgame_nextcard
  ENDIF
  IF InPlay% THEN
   IF mx%>=DP% AND mx%<=DP%+CW%-6 AND my%>DY% AND my%<DY%+CH% THEN
    PROCgame_dragcard("P",x%,y%)
   ENDIF
   IF mx%>=TX% AND mx%<TX%+CW%*7 AND my%<TY% THEN
    col%=(mx%-TX%) DIV CW%
    hlen%=0
    IF LEN(Table$(col%+1))>1 THEN hlen%=HH%*(LEN(Table$(col%+1))-1)
    IF my%>TY%-hlen% THEN Index%=(-(my%-TY%) DIV HH%)+1
    IF my%<TY%-hlen% AND my%>TY%-hlen%-CH% THEN Index%=LEN(Table$(col%+1))
    min%=0
    REPEAT
     min%+=1
    UNTIL ASCMID$(Table$(col%+1),min%)<128
    IF Index%>=min% AND Index%<=LEN(Table$(col%+1)) THEN
     PROCgame_dragcard(CHR$((mx%-TX%) DIV CW%+ASC"A"),x%,y%)
    ENDIF
   ENDIF
  ENDIF
 WHEN ADJUST%:
  IF InPlay% THEN
   IF mx%>DP% AND mx%<DP%+CW% AND my%>DY% AND my%<DY%+CH% THEN
    IF NOT FNgame_dragcheck("P") THEN PROCgame_move("S")
   ENDIF
   IF mx%>TX% AND mx%<TX%+CW%*7 THEN
    c$=CHR$((mx%-TX%) DIV CW%+ASC"A")
    IF NOT FNgame_dragcheck(c$) THEN PROCgame_move("S")
   ENDIF
  ENDIF
ENDCASE
ENDPROC

DEF PROCgamewin_keypress (key%)
REM Handle keypresses in the main window
CASE key% OF
 WHEN &181:OSCLI "Filer_Run <Patience$Dir>.!Help"
 WHEN &182:PROCgame_newgame
 WHEN &1A2:Block%!0=GameWin%:SYS"Wimp_CloseWindow",,Block%
 WHEN &1B:PROCgame_resign
 OTHERWISE:SYS "Wimp_ProcessKey",key%
ENDCASE
ENDPROC

DEF FNgamewin_update (xmin%,ymin%,xmax%,ymax%)
REM Mark an area of the main window for redraw
!Block%=GameWin%
Block%!4=xmin%:Block%!8=ymin%
Block%!12=xmax%:Block%!16=ymax%
SYS "Wimp_UpdateWindow",,Block% TO more%
Wx%=Block%!4-Block%!20:Wy%=Block%!16-Block%!24
=more%

DEF PROCgamewin_store
REM Stores the position of the game window
LOCAL obj%,file%
SYS "XOS_File",17,"Choices:Patience" TO obj%
IF obj%=0 THEN SYS "XOS_File",8,"<Choices$Write>.Patience"
file%=OPENOUT "<Choices$Write>.Patience.WinPos"
Block%!0=GameWin%
SYS "Wimp_GetWindowState",,Block%
BPUT#file%,"WinX:"+STR$(Block%!4)
BPUT#file%,"WinY:"+STR$(Block%!8)
BPUT#file%,"ExtentX:"+STR$(Block%!12-Block%!4)
BPUT#file%,"ExtentY:"+STR$(Block%!16-Block%!8)
CLOSE#file%
ENDPROC

REM-----------------------------------------------------------------------------
REM // Choicewin //
REM-----------------------------------------------------------------------------

DEF PROCchoicewin_init (xmouse%)
REM Initialise and open Choices window
PROCwimp_selecticon(ChoiceWin%,4,Option_OnlyKings%)
PROCwimp_selecticon(ChoiceWin%,5,Option_RevCards%)
PROCwimp_selecticon(ChoiceWin%,7,Option_Sound%)
CASE Option_NumberOver% OF
 WHEN 3:PROCwimp_selecticon(ChoiceWin%,9,TRUE)
  PROCwimp_selecticon(ChoiceWin%,10,FALSE)
 WHEN 1:PROCwimp_selecticon(ChoiceWin%,9,FALSE)
  PROCwimp_selecticon(ChoiceWin%,10,TRUE)
ENDCASE
PROCwimp_openwindow(ChoiceWin%,xmouse%-220,140,xmouse%-220+594,140+540)
SYS "Wimp_SetCaretPosition",ChoiceWin%,-1,0,0,1<<25
ENDPROC

DEF PROCchoicewin_click (icon%,button%)
REM Handle Select clicks in Choices window
LOCAL obj%,file%,write$
SYS "Wimp_SetCaretPosition",ChoiceWin%,-1,0,0,1<<25
IF button%=MENU% THEN ENDPROC
CASE icon% OF
 WHEN 1:
  IF button%=SELECT% THEN
   Block%!0=ChoiceWin%:SYS "Wimp_CloseWindow",,Block%
  ENDIF
 WHEN 0:
  Option_OnlyKings%=FNwimp_getselect(ChoiceWin%,4)
  Option_RevCards%=FNwimp_getselect(ChoiceWin%,5)
  Option_Sound%=FNwimp_getselect(ChoiceWin%,7)
  IF FNwimp_getselect(ChoiceWin%,9) THEN
   Option_NumberOver%=3
  ELSE
   Option_NumberOver%=1
  ENDIF
  SYS "XOS_File",17,"Choices:Patience" TO obj%
  IF obj%=0 THEN SYS "XOS_File",8,"<Choices$Write>.Patience"
  file%=OPENOUT "<Choices$Write>.Patience.Choices"
   IF Option_RevCards%=FALSE THEN write$="RevCards:N" ELSE write$="RevCards:Y"
   BPUT#file%,write$
   IF Option_OnlyKings%=FALSE THEN write$="OnlyKings:N" ELSE write$="OnlyKings:Y"
   BPUT#file%,write$
   IF Option_Sound%=FALSE THEN write$="Sound:N" ELSE write$="Sound:Y"
   BPUT#file%,write$
   BPUT#file%,"NumberOver:"+STR$Option_NumberOver%
  CLOSE#file%
  IF button%=SELECT% THEN Block%!0=ChoiceWin%:SYS "Wimp_CloseWindow",,Block%
ENDCASE
ENDPROC

DEF PROCchoicewin_keypress (key%)
REM Handle keypresses in Choices window
CASE key% OF
 WHEN &D:PROCchoicewin_click(0,SELECT%):REM OK
 WHEN &1B,&1A2:PROCchoicewin_click(1,SELECT%):REM Cancel
 OTHERWISE:SYS "Wimp_ProcessKey",key%
ENDCASE
ENDPROC

REM-----------------------------------------------------------------------------
REM // Iconbar //
REM-----------------------------------------------------------------------------

DEF PROCiconbar_click (button%)
REM Handle clicks on the iconbar icon
CASE button% OF
 WHEN MENU%:
  SYS "Wimp_GetPointerInfo",,Block%
  SYS "Wimp_CreateMenu",,IconMenu%,!Block%-64,(4*44)+96
  MenuOpen%=IconMenu%
 WHEN SELECT%,ADJUST%:
  IF FirstOpen%=TRUE THEN
   PROCgamewin_init
   PROCwimp_openwindow(GameWin%,Option_WinX%,Option_WinY%,Option_WinX%+Option_ExtentX%,Option_WinY%+Option_ExtentY%)
   SYS "Wimp_SetCaretPosition",GameWin%,-1,0,0,1<<25
   FirstOpen%=FALSE
  ELSE
   PROCwimp_openwindow(GameWin%,-1,-1,-1,-1)
   SYS "Wimp_SetCaretPosition",GameWin%,-1,0,0,1<<25
  ENDIF
ENDCASE
ENDPROC

REM-----------------------------------------------------------------------------
REM // Wimp //
REM-----------------------------------------------------------------------------

DEF PROCwimp_init
REM Load Resources and initialise app
DIM Block% 255                         :REM General purpose buffer
DIM MessText% 255                      :REM Messages buffer
DIM ErrorBlock% 255                    :REM Error buffer
AppQuit%=FALSE                         :REM Quit flag
AppHandle%=0                           :REM Application handle
IconHandle%=0                          :REM Iconbar handle
DIM WindowName% 12                     :REM Buffer for Window title
DIM Factors% 15,PixTrans% 255          :REM Sprite plotting data
MenuOpen%=0                            :REM Open menu handle
FirstOpen%=TRUE                        :REM Flag for first opening of window
Font%=0                                :REM Font handle
MESSAGE_QUIT%=&0                       :REM Message constants
MESSAGE_SAVEDESKTOP%=&A                :REM Message constants
MESSAGE_MODECHANGE%=&400C1             :REM Message constants
MESSAGE_HELPREQUEST%=&502              :REM Message constants
MESSAGE_HELPREPLY%=&503                :REM Message constants
SWI_SPRITE%=&2E                        :REM SWI OS_SpriteOp
SWI_PIXTRANS%=&400ED                   :REM SWI Wimp_ReadPixTrans
SWI_GENTABLE%=&40763                   :REM SWI ColourTrans_GenerateTable
SWI_FONTCOLOURS%=&4074F                :REM SWI ColourTrans_SetFontColours
SWI_FONTPAINT%=&40086                  :REM SWI Font_Paint
SWI_SETFONT%=&4008A                    :REM SWI Font_SetFont
SELECT%=4                              :REM Mouse button constants
MENU%=2                                :REM Mouse button constants
ADJUST%=1                              :REM Mouse button constants
Iconbar%=-2                            :REM Iconbar icon handle
SolidDrag%=FALSE                       :REM CMOS Solid-drag bit set/clear
Block%!0=MESSAGE_SAVEDESKTOP%
Block%!4=MESSAGE_MODECHANGE%
Block%!8=MESSAGE_HELPREQUEST%
Block%!12=MESSAGE_HELPREPLY%
Block%!16=0
SYS "Wimp_Initialise",380,&4B534154,FNmess_get("_TaskName"),Block% TO,AppHandle%
SYS "OS_Byte",129,0,&FF TO,OSVersion%
ON ERROR PROCwimp_error
!Block%=-1:Block%!4=0:Block%!8=0:Block%!12=68:Block%!16=68
Block%!20=%11000000000010:$(Block%+24)="!"+FNmess_get("_TaskName")
SYS "Wimp_CreateIcon",,Block% TO IconHandle%
PROCwimp_sprites
WindowArea%=FNwimp_opentemplates("<Patience$Dir>.Templates")
InfoWin%=FNwimp_loadtemplate("Info",WindowArea%)
GameWin%=FNwimp_loadtemplate("Game",WindowArea%)
ChoiceWin%=FNwimp_loadtemplate("Choices",WindowArea%)
ToolWin%=FNwimp_loadtemplate("Toolbar",WindowArea%)
PROCwimp_menus
SYS "Font_FindFont",,"Homerton.Medium",12*16,12*16,0,0 TO Font%
PROCwimp_seticontext(InfoWin%,4,FNmess_get("_Version"))
ENDPROC

DEF FNwimp_opentemplates (path$)
REM Opens a Template file and creates buffer for total indirected data
LOCAL size%,largest%,next%,indarea%,indsize%,totalindsize%
SYS "Wimp_OpenTemplate",,path$
largest%=0:totalindsize%=0
$WindowName%="*"
SYS "Wimp_LoadTemplate",,0,,,,WindowName%,0 TO ,size%,indsize%,,,,next%
IF size%>largest% THEN largest%=size%
IF indsize%>0 THEN totalindsize%=totalindsize%+indsize%
WHILE next%<>0
 $WindowName%="*"
 SYS "Wimp_LoadTemplate",,0,,,,WindowName%,next% TO ,size%,indsize%,,,,next%
 IF next%<>0 THEN
  IF size%>largest% THEN largest%=size%
  IF indsize%>0 THEN totalindsize%=totalindsize%+indsize%
 ENDIF
ENDWHILE
DIM indarea% totalindsize%+16
DIM WindowBlock% largest%+16
WindowIndPtr%=indarea%
=indarea%

DEF FNwimp_loadtemplate (name$,indarea%)
REM Loads a window definition from a Template file
LOCAL indsize%
$WindowName%=name$
SYS "Wimp_LoadTemplate",,0,,,,WindowName% TO ,,indsize%
SYS "Wimp_LoadTemplate",,WindowBlock%,WindowIndPtr%,WindowIndPtr%+indsize%,-1,WindowName% TO ,,WindowIndPtr%
WindowBlock%!64=SpriteArea%
SYS "Wimp_CreateWindow",,WindowBlock% TO window%
=window%

DEF PROCwimp_sprites
REM Load application sprites
LOCAL spritefile%,spritesize%,spritepath$,dragsize%,pix%
REM Load best sprite file for mode into Sprite Area
SYS "Wimp_ReadSysInfo",2 TO pix%
spritepath$="<Patience$Dir>.Sprites"+CHR$?pix%+CHR$pix%?1
spritefile%=OPENIN spritepath$
IF spritefile%=0 THEN spritepath$="<Patience$Dir>.Sprites":spritefile%=OPENIN spritepath$
spritesize%=EXT#spritefile%+16:CLOSE#spritefile%
DIM SpriteArea% spritesize%
!SpriteArea%=spritesize%:SpriteArea%!8=16
SYS SWI_SPRITE%,&109,SpriteArea%
SYS SWI_SPRITE%,&10A,SpriteArea%,spritepath$
END=END+&8000:REM Give back unused sprite memory
ENDPROC

DEF PROCwimp_menus
REM Create menus
IconMenu%=FNwimp_makemenu(FNmess_get("_TaskName"),4)
PROCwimp_additem(IconMenu%,0,0,InfoWin%,FNmess_get("Info"))
PROCwimp_additem(IconMenu%,1,0,-1,FNmess_get("HelpIcon"))
PROCwimp_additem(IconMenu%,2,0,-1,FNmess_get("Choices"))
PROCwimp_additem(IconMenu%,3,128,-1,FNmess_get("Quit"))
MainMenu%=FNwimp_makemenu(FNmess_get("_TaskName"),4)
PROCwimp_additem(MainMenu%,0,0,-1,FNmess_get("DealHand"))
PROCwimp_additem(MainMenu%,1,2,-1,FNmess_get("Resign"))
PROCwimp_additem(MainMenu%,2,2,-1,FNmess_get("Remember"))
PROCwimp_additem(MainMenu%,3,128,-1,FNmess_get("Help"))
ENDPROC

DEF FNwimp_makemenu (title$,items%)
REM Create a menu
LOCAL buffer%,tbuffer%
DIM buffer% 28+(24*items%)
IF LEN(title$)>12 THEN
 DIM tbuffer% LEN(title$)+1
 buffer%!0=tbuffer%
 buffer%!4=0
 buffer%!8=LEN(title$)+1
 $(tbuffer%)=title$
ELSE
 $(buffer%)=title$
ENDIF
buffer%!12=&00070207
buffer%!16=80
buffer%!20=44
buffer%!24=0
=buffer%

DEF PROCwimp_additem (menu%,item%,flags%,link%,text$)
REM Add a menu item
LOCAL buffer%
menu%!(28+(item%*24))=flags%
menu%!(28+(item%*24)+4)=link%
IF LEN(text$)>12 THEN
 DIM buffer% LEN(text$)+1
 menu%!(28+(item%*24)+8)=&07000121
 menu%!(28+(item%*24)+12)=buffer%
 menu%!(28+(item%*24)+16)=0
 menu%!(28+(item%*24)+20)=LEN(text$)+1
 $(buffer%)=text$
ELSE
 menu%!(28+(item%*24)+8)=&07000021
 $(menu%+28+(item%*24)+12)=text$
ENDIF
ENDPROC

DEF PROCwimp_poll
REM Respond to Wimp events
LOCAL reason%
SYS "Wimp_Poll",1,Block% TO reason%
CASE reason% OF
 WHEN 1:PROCgame_drawboard
 WHEN 2:SYS "Wimp_OpenWindow",,Block%
 WHEN 3:SYS "Wimp_CloseWindow",,Block%
 WHEN 6:PROCwimp_click(Block%!0,Block%!4,Block%!8,Block%!12,Block%!16)
 WHEN 7:PROCwimp_dragbox
 WHEN 8:PROCwimp_keypress(Block%!0,Block%!24)
 WHEN 9:PROCwimp_menuselect(Block%)
 WHEN 17,18:PROCwimp_messages(Block%)
ENDCASE
ENDPROC

DEF PROCwimp_click (x%,y%,button%,window%,icon%)
REM Handle mouse clicks
CASE window% OF
 WHEN GameWin%:PROCgamewin_click(x%,y%,button%)
 WHEN Iconbar%:PROCiconbar_click(button%)
 WHEN ChoiceWin%:PROCchoicewin_click(icon%,button%)
ENDCASE
ENDPROC

DEF PROCwimp_dragbox
REM Handle drags ending in the main window
SYS "Wimp_GetPointerInfo",,Block%
x%=Block%!0:y%=Block%!4:window%=Block%!12
IF window%=GameWin% THEN
 IF SolidDrag% THEN
  SYS "DragASprite_Stop"
 ELSE
  SYS "Wimp_DragBox",,-1
 ENDIF
 Block%!0=GameWin%
 SYS "Wimp_GetWindowState",,Block%
 Wx%=Block%!4-Block%!20:Wy%=Block%!16-Block%!24
 PROCgame_dropcard(x%,y%)
ENDIF
ENDPROC

DEF PROCwimp_keypress (window%,key%)
REM Handle keypresses
CASE window% OF
 WHEN GameWin%:PROCgamewin_keypress(key%)
 WHEN ChoiceWin%:PROCchoicewin_keypress(key%)
ENDCASE
ENDPROC

DEF PROCwimp_menuselect (menus%)
REM Handle menu selections
SYS "Wimp_DecodeMenu",,MenuOpen%,Block%,ErrorBlock%
SYS "Wimp_GetPointerInfo",,Block%
CASE MenuOpen% OF
 WHEN MainMenu%
  CASE $(ErrorBlock%) OF
   WHEN FNmess_get("Resign"):PROCgame_resign
   WHEN FNmess_get("DealHand"):PROCgame_newgame
   WHEN FNmess_get("Remember"):PROCgamewin_store
   WHEN FNmess_get("Help"):OSCLI "Filer_Run <Patience$Dir>.!Help"
  ENDCASE
 WHEN IconMenu%:
  CASE $(ErrorBlock%) OF
   WHEN FNmess_get("HelpIcon"):OSCLI "Filer_Run <Patience$Dir>.!Help"
   WHEN FNmess_get("Choices"):PROCchoicewin_init(Block%!0)
   WHEN FNmess_get("Quit"):AppQuit%=TRUE
  ENDCASE
ENDCASE
IF Block%!8 AND 1 THEN SYS "Wimp_CreateMenu",,MenuOpen%
ENDPROC

DEF PROCwimp_messages (Block%)
REM Handle Wimp messages
LOCAL file%,len%,pix%,helptext$
CASE Block%!16 OF
 WHEN MESSAGE_HELPREQUEST%:
  helptext$=FNwimp_gethelp
  IF LEN(helptext$)>0 THEN
   Block%!0=256
   Block%!12=Block%!8
   Block%!16=MESSAGE_HELPREPLY%
   $(Block%+20)=helptext$+CHR$0
   SYS "Wimp_SendMessage",17,Block%,Block%!4
  ENDIF
 WHEN MESSAGE_QUIT%:
  AppQuit%=TRUE
 WHEN MESSAGE_SAVEDESKTOP%:
  file%=Block%!20
  SYS "XOS_ReadVarVal","Patience$Dir",Block%,256,0,3 TO ,,len%
  Block%?len%=13
  BPUT#file%,"Run "+$(Block%)
 WHEN MESSAGE_MODECHANGE%:
  SYS "Font_LoseFont",Font%
  SYS "Font_FindFont",,"Homerton.Medium",12*16,12*16,0,0 TO Font%
ENDCASE
ENDPROC

DEF PROCwimp_openwindow (window%,x%,y%,w%,h%)
REM Opens a window at a specific position
LOCAL height%
Block%!0=window%
SYS "Wimp_GetWindowState",,Block%
IF x%>-1 THEN Block%!4=x%:IF y%>-1 THEN Block%!8=y%
IF w%>-1 THEN Block%!12=w%:IF h%>-1 THEN Block%!16=h%
Block%!28=-1
CASE window% OF
 WHEN GameWin%:
  Block%!0=GameWin%
  SYS "Wimp_OpenWindow",,Block%,&4B534154,-1
  height%=Block%!16
  Block%!0=ToolWin%
  SYS "Wimp_GetWindowState",,Block%
  Block%!8=height%-60
  Block%!16=height%
  SYS "Wimp_OpenWindow",,Block%,&4B534154,GameWin%
 OTHERWISE:SYS "Wimp_OpenWindow",,Block%
ENDCASE
ENDPROC

DEF PROCwimp_seticontext (window%,icon%,text$)
REM Set the text label in an icon
Block%!0=window%:Block%!4=icon%
SYS "Wimp_GetIconState",,Block%
$(Block%!28)=text$
Block%!8=0:Block%!12=0
SYS "Wimp_SetIconState",,Block%
ENDPROC

DEF PROCwimp_selecticon (window%,icon%,action%)
REM Selects an icon
Block%!0=window%:Block%!4=icon%
SYS "Wimp_GetIconState",,Block%
IF action% THEN
 IF NOT(Block%!24 AND 1<<21) THEN
  Block%!8=1<<21:Block%!12=1<<21
  SYS "Wimp_SetIconState",,Block%
 ENDIF
ELSE
 IF (Block%!24 AND 1<<21) THEN
  Block%!8=0:Block%!12=1<<21
  SYS "Wimp_SetIconState",,Block%
 ENDIF
ENDIF
ENDPROC

DEF FNwimp_getselect (window%,icon%)
REM Returns TRUE if icon is selected, FALSE if not
Block%!0=window%:Block%!4=icon%
SYS "Wimp_GetIconState",,Block%
IF (Block%!24 AND (1<<21)) THEN =TRUE ELSE =FALSE

DEF PROCwimp_error
REM Report fatal errors and close
IF SolidDrag% THEN
 SYS "DragASprite_Stop"
ELSE
 SYS "Wimp_DragBox",,-1
ENDIF
ErrorBlock%!0=ERR
$(ErrorBlock%+4)=REPORT$+" "+FNmess_getargs("EL",STR$ERL,"")+CHR$0
SYS "Wimp_ReportError",ErrorBlock%,1,FNmess_get("_TaskName")
END
ENDPROC

DEF PROCwimp_pixtrans (area%,name$)
REM Sets up scale factors and colour translation for sprites
LOCAL spointer%
SYS SWI_SPRITE%,&118,area%,name$ TO ,,spointer%
SYS SWI_PIXTRANS%,&200,area%,spointer%,,,,Factors%
SYS SWI_GENTABLE%,area%,spointer%,-1,-1,PixTrans%,3
ENDPROC

DEF PROCwimp_plotsprite (area%,name$,x%,y%,mask%)
REM Plots a sprite at coordinates x%,y%
LOCAL sflags%
IF OSVersion% >= &A5 THEN sflags%=16
IF mask% THEN sflags%=sflags%+8
SYS SWI_SPRITE%,&134,area%,name$,x%,y%,sflags%,Factors%,PixTrans%
ENDPROC

DEF FNwimp_gethelp
LOCAL x%,y%,text$
CASE Block%!32 OF
 WHEN -2:
  IF Block%!36=IconHandle% THEN text$=FNmess_get("H1")
 WHEN InfoWin%:
  text$=FNmess_get("H2")
 WHEN GameWin%:
  x%=Block%!20:y%=Block%!24
  ErrorBlock%!0=GameWin%
  SYS "Wimp_GetWindowState",,ErrorBlock%
  Wx%=ErrorBlock%!4:Wy%=ErrorBlock%!16
  IF x%-Wx%>DP% AND x%-Wx%<DP%+CW% AND y%-Wy%>DY% AND y%-Wy%<DY%+CH% THEN text$=FNmess_get("G1")
  IF x%-Wx%>DX% AND x%-Wx%<DX%+CW% AND y%-Wy%>DY% AND y%-Wy%<DY%+CH% THEN text$=FNmess_get("G2")
  IF x%-Wx%>SX% AND x%-Wx%<SX%+CW%*4 AND y%-Wy%>TY%-CH% AND y%-Wy%<TY% THEN text$=FNmess_get("G3")
  IF x%-Wx%>TX% AND x%-Wx%<TX%+CW%*7 AND y%-Wy%<TY% THEN text$=FNmess_get("G4")
  IF text$="" THEN text$=FNmess_get("G0")
 WHEN ChoiceWin%:
  CASE Block%!36 OF
   WHEN 0,1,4,5,7,9,10,11:
    text$=FNmess_get("C"+STR$(Block%!36))
  ENDCASE
 OTHERWISE:
  SYS "Wimp_GetMenuState",1,ErrorBlock%,Block%!32,Block%!36
  CASE MenuOpen% OF
   WHEN IconMenu%:
    text$=FNmess_get("M"+STR$(!ErrorBlock%))
   WHEN MainMenu%:
    IF ErrorBlock%!0>-1 THEN text$=FNmess_get("M"+STR$(!ErrorBlock%+4))
  ENDCASE
ENDCASE
=text$

REM-----------------------------------------------------------------------------
REM // Messages //
REM-----------------------------------------------------------------------------

DEF PROCmess_load (name$)
REM Loads Message file
LOCAL ERROR
ON ERROR LOCAL RESTORE ERROR:ERROR 3,REPORT$
SYS "MessageTrans_FileInfo",,name$
SYS "OS_Module",6,,,17+LEN(name$) TO ,,MessDesc%
$(MessDesc%+16)=name$
SYS "MessageTrans_OpenFile",MessDesc%,MessDesc%+16
ENDPROC

DEF PROCmess_close
REM Close message file and free memory
SYS "XMessageTrans_CloseFile",MessDesc%
SYS "XOS_Module",7,,MessDesc%
ENDPROC

DEF FNmess_get (tag$)
REM Retrieves a Message string from a tag
=FNmess_getargs(tag$,"","")

DEF FNmess_getargs (tag$,arg1$,arg2$)
REM Retrieves a Message string with arguments
LOCAL flags%,args%
SYS "XMessageTrans_Lookup",MessDesc%,tag$,MessText%,256,arg1$,arg2$ TO ,,,args%;flags%
IF flags% AND 1 THEN args%=0
MessText%?args%=13
=$MessText%
