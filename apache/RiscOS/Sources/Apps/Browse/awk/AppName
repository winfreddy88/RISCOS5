# Generate an assembler header file containing ApplicationName
# and ApplicationVersion using information in the Messages file.
# Component version and date need only be updated in Messages file
#
# 06-Jun-1997 (BAL): Made generic for toolbox apps. Extracts app name
#                    from _TaskName tag and gets version number and date
#                    from Version tag.
#
# 26-Apr-2000 (ADH): Modified for "dd mmm yyyy" format rather than out
#                    dated "dd-mmm-yy".
#
# 26-Apr-2005 (ADH): Updated to work in modern build systems under gawk.

BEGIN {
    name_found = 0
    version_found = 0
}

{
    if (!version_found) {
        split($1, Date, ":")
        if (Date[1] == "Version") {
            version_found = 1
            Date[2] = $2
            Date[3] = $3
            Date[4] = $4
            Date[5] = $5
        }
    }
    if (!name_found) {
        split($1, Name, ":")
        if (Name[1] == "_TaskName") {
            name_found = 1
        }
    }
}

END {
    printf("; AppName - generated from Messages, used by ModuleWrap\n")
    printf("\n")
    printf("                   GBLS    ApplicationName\n")
    printf("                   GBLS    ApplicationVersion\n")
    printf("\n")
    printf("ApplicationName    SETS    \"%s\"\n", Name[2])
    printf("ApplicationVersion SETS    \"%s %s %s\"\n", Date[2], Date[3], Date[4])
    printf("\n")
    printf("                   END\n")
}

