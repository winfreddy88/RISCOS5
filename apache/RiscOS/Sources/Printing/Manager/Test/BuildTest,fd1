REM Copyright 1996 Acorn Computers Ltd
REM
REM Licensed under the Apache License, Version 2.0 (the "License");
REM you may not use this file except in compliance with the License.
REM You may obtain a copy of the License at
REM
REM     http://www.apache.org/licenses/LICENSE-2.0
REM
REM Unless required by applicable law or agreed to in writing, software
REM distributed under the License is distributed on an "AS IS" BASIS,
REM WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
REM See the License for the specific language governing permissions and
REM limitations under the License.
REM

Start=0
In_Word=1
End_Of_Word=2
End_Of_Sentence=3
End_Of_Paragraph=4
End_Of_File=5

INPUT "File size: " size%

h%=OPENOUT STR$ size%
ON ERROR CLOSE#h%: END

out%=0
col%=0

state%=Start
REPEAT
  IF size%-out%<=6 THEN
    FOR i%=out%+1 TO size%-2
      BPUT#h%,ASC "a"+RND(26)-1
    NEXT
    BPUT#h%,ASC "."
    BPUT#h%,10
    state%=End_Of_File
  ELSE
    CASE state% OF
      WHEN Start
        BPUT#h%,"   ";
        BPUT#h%,ASC "A"+RND(26)-1
        out%+=4
        col%=4
        state%=In_Word
      WHEN In_Word
        IF RND(5)=1 THEN
          state%=End_Of_Word
        ELSE
          BPUT#h%,ASC "a"+RND(26)-1
          out%+=1
          col%+=1
        ENDIF
      WHEN End_Of_Word
        IF RND(10)=1 THEN
          state%=End_Of_Sentence
        ELSE
          IF col%<=60 THEN
            BPUT#h%,ASC " "
            col%+=1
            out%+=1
          ELSE
            BPUT#h%,10
            col%=0
            out%+=1
          ENDIF
          BPUT#h%,ASC "a"+RND(26)-1
          out%+=1
          col%+=1
          state%=In_Word
        ENDIF
      WHEN End_Of_Sentence
        IF RND(3)=1 THEN
          state%=End_Of_Paragraph
        ELSE
          BPUT#h%,". ";
          BPUT#h%,ASC "A"+RND(26)-1
          out%+=3
          col%+=3
          state%=In_Word
        ENDIF
      WHEN End_Of_Paragraph
        BPUT#h%,ASC "."
        BPUT#h%,10
        BPUT#h%,10
        BPUT#h%,"   ";
        BPUT#h%,ASC "A"+RND(26)-1
        out%+=7
        col%=4
        state%=In_Word
    ENDCASE
  ENDIF
UNTIL state%=End_Of_File

CLOSE#h%
SYS "OS_File",18,STR$ size%,&FFF
