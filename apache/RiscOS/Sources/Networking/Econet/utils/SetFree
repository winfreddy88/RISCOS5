; Copyright 2016 Castle Technology Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;

        GET     Hdr:ListOpts
        GET     Hdr:Macros
        GET     Hdr:Services
        GET     Hdr:System
        GET     Hdr:FSNumbers
        GET     Hdr:NewErrors
        GET     Hdr:Econet

        TTL     SetFree

        AREA    |SetFree$$Code|, CODE, READONLY, PIC

                ^ 0, R12
Wk_CallBuffer   # 0
Wk_FreeAmount   # 4
Wk_UserName     # 26
Wk_TotalSize    * :INDEX: @
        ASSERT  Wk_TotalSize <= 1024                    ; Utility workspace limit

; SetFree
Utility_Code
        LDRB    R0,[R1],#1
        TEQ     R0,#0
        BEQ     syntaxerror

        ADR     R4,Wk_CallBuffer
        MOV     R2,#:INDEX:Wk_UserName
loop
        STRB    R0,[R4,R2]                              ; Copy in <Username>
        ADD     R2,R2,#1
        LDRB    R0,[R1],#1
        CMP     R0,#&20
        BGT     loop
        
        MOV     R0,#&0D
        STRB    R0,[R4,R2]
        ADD     R6,R2,#1
        SUB     R1,R1,#1
loop_1
        LDRB    R0,[R1],#1                              ; Skip spaces
        CMP     R0,#&20
        BEQ     loop_1
        BLT     syntaxerror

        SUB     R1,R1,#1
        MOV     R0,#&10
        SWI     XOS_ReadUnsigned                        ; Parse <FreeSpace>
        MOVVS   PC,R14
        STR     R2,Wk_FreeAmount

        MOV     R0,#FileServer_SetUserFreeSpace
        ADR     R1,Wk_CallBuffer
        MOV     R2,R6
        MOV     R3,#?Wk_FreeAmount + ?Wk_UserName
        SWI     XNetFS_DoFSOp
        MOV     PC,R14
syntaxerror
        ADR     R0,ErrorBlock_Syntax
        ORRS    PC,R14,#V_bit                           ; Warning: May be unsafe in 32-bit modes

ErrorBlock_Syntax
        DCD     ErrorNumber_Syntax
        DCB     "Syntax: *SetFree <Username> <FreeSpace (in hex)>", 0
        ALIGN
        
        DCB     "SetFree", 9, 9, "2.01 (23 Mar 1988)", 0

        END
