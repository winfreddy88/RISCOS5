; Copyright 1998 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
;
;  s.Handlers - routines to handle environment handlers
;


;
; All I wanted was an Error handler, but page 1-294 of the PRMs says
; we need an Upcall and Exit handler as well to remove the error
; handler...
;

        ASSERT  OldExitHandler-OldUpcallHandler = 12
        ASSERT  OldErrorHandler-OldExitHandler = 12

InstallHandlers
        Entry   "R0-R4"
        ADR     R4, OldUpcallHandler
        ; Install an Upcall handler (see 1-294)
        MOV     R0, #16
        ADR     R1, UpcallHandler
        MOV     R2, R12
        SWI     OS_ChangeEnvironment
        STMIA   R4!, {R1-R3}
        ; and an Exit handler
        MOV     R0, #11
        ADR     R1, ExitHandler
        MOV     R2, R12
        SWI     OS_ChangeEnvironment
        STMIA   R4!, {R1-R3}
        ; and an Error handler
        MOV     R0, #6
        ADR     R1, ErrorHandler
        MOV     R2, R12
        LDR     R3, mystack
        SWI     OS_ChangeEnvironment
        STMIA   R4!, {R1-R3}
        EXIT

RestoreHandlers
        Entry   "R0-R4"
        ADR     R4, OldUpcallHandler
        MOV     R0, #16
        LDMIA   R4!, {R1-R3}
        SWI     XOS_ChangeEnvironment
        MOV     R0, #11
        LDMIA   R4!, {R1-R3}
        SWI     XOS_ChangeEnvironment
        MOV     R0, #6
        LDMIA   R4!, {R1-R3}
        SWI     XOS_ChangeEnvironment
        EXIT

ErrorHandler
        MOV     R12, R0
        LDR     R13, mystack
        ADD     R13, R13, #UserStackSize
        ADR     R0, MessagesBlock
        addr    R1, clipboard_text
        ADR     R2, PollBlock
        MOV     R3, #?PollBlock
        SWI     XMessageTrans_Lookup            ; R2->task name
      [ Debug
        LDR     R1, mystack
        LDR     R0, [R1], #8
01      LDRB    R2, [R1], #1
        CMP     R2, #' '
        BGE     %BT01
        MOV     R2, #' '
        STRB    R2, [R1, #-1]   ; separating space
        SWI     XOS_ConvertHex8 ; append error's PC to message
      ]
        ADR     R0, MessagesBlock
        ADR     R1, quit_text
        MOV     R2, #0
        SWI     XMessageTrans_Lookup
        MOV     R5, R2          ; Quit button instead of Cancel
        LDR     R0, mystack
        ADD     R0, R0, #4
        LDR     R1, =&101:OR:(2<<9):OR:(3<<28)
        ADR     R2, PollBlock
        MOV     R3, #0
        MOV     R4, #0
        SWI     XWimp_ReportError
        TEQ     R1, #1
        SWINE   XOS_Exit
        B       PollLoop

quit_text
        =       "Quit", 0
        ALIGN

ExitHandler
        LDR     R13, mystack
        ADD     R13, R13, #UserStackSize
        BL      CloseDown
        SWI     OS_Exit

UpcallHandler
        EntryS
        TEQ     R0, #256                 ; UpCall_NewApplication?
        BLEQ    CloseDown
        EXITS

        LTORG
        END
