; Copyright 1996 Acorn Computers Ltd
;
; Licensed under the Apache License, Version 2.0 (the "License");
; you may not use this file except in compliance with the License.
; You may obtain a copy of the License at
;
;     http://www.apache.org/licenses/LICENSE-2.0
;
; Unless required by applicable law or agreed to in writing, software
; distributed under the License is distributed on an "AS IS" BASIS,
; WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
; See the License for the specific language governing permissions and
; limitations under the License.
;
; ->Error

;***************************************************************************
PutByte
;***************************************************************************

        PushAllWithReturnFrame

        Debug   fs,"Put byte"
        
        ; Fall through

;*************************************************************************
SaveFile     ; also deals with other write operations
;*************************************************************************

        MOV     r0, #ERROR_WRITETOCD
        ; Fall through




;---------------------------------------------------------------------------------------------
; Every error that occurs should end up here
;---------------------------------------------------------------------------------------------

ErrorExit

;----------------------------------------------------------------------------------
; If r0 < 0 THEN it indicates one of my errors that I must fetch using MessageTrans
; Requirements:
;              r0  =  negative error number
;              r12 -> workspace
;              r13 -> FD stack
;----------------------------------------------------------------------------------

        CMP     r0,#-26
        BLO     error_riscos2
        ; Error pointers are still valid in most of the top half of memory!


        RSB     r3, r0, #0
        ADR     r0, TempArea

        ; Build error number and message tag
        LDR     r2, =CDFS_ErrorBlock - 1
        ADD     r2, r2, r3

        ADD     r3, r3, #"a" - 1
        MOV     r3, r3, LSL #8
        ORR     r3, r3, #"e"

        ; Error number, Token - 'e?'<null>
        STMIA   r0, { r2, r3 }

;----------------------------------------------------------------------------------
; Get error message from message file
;----------------------------------------------------------------------------------

        ADR     r1, message_block
        MOV     r2, #0
        SWI     XMessageTrans_ErrorLookup

        SETV
        PullAllFromFrameAndExit AL, 1

error_riscos2

;***********************
; This is a trap to change 'Bad Mode' into a decent error message
;***********************

        ; Bad mode = Audio disc

        LDR     R1, [ R0 ]              ; R1 = error number
        LDR     R2, =CDFSDRIVERERROR__WRONG_DATA_MODE
        TEQ     R1, R2

        MOVEQ   r0, #ERROR_AUDIODISC
        BEQ     ErrorExit

        SETV
        PullAllFromFrameAndExit AL, 1

        LTORG

        END
